ACTOR AstroShotgun : NaziAstroWeapon
{
	//$Title (3) Astrostein Shotgun
	Weapon.SelectionOrder 400
	Weapon.AmmoType "AstroShotgunLoaded"
	Weapon.AmmoUse 1
	Weapon.AmmoType2 "AstroShotgunShell"
	Weapon.AmmoUse2 1
	Weapon.AmmoGive2 8
 	Weapon.UpSound "shotgun/select"
 	Inventory.PickupMessage "$PROTON"
	Obituary "$OBPROTON"
	Tag "$TAGPROTF"
	States
	{
  	Select:
		ASSG B 0 A_Raise
		ASSG B 1 A_Raise
		Loop
  	Deselect:
		ASSG B 0 A_Lower
		ASSG B 1 A_Lower
		Loop
	Ready:
		ASSG B 0 A_JumpIfInventory("AstroShotgunLoaded",0,2)
		ASSG B 0 A_JumpIfInventory("AstroShotgunShell",1,2)
		ASSG B 1 A_WeaponReady
		Loop
		ASSG B 1 A_WeaponReady(WRF_ALLOWRELOAD)
		Loop
	Fire:
		ASSG B 0 A_JumpIfInventory("AstroShotgunLoaded",1,1)
		Goto Dryfire
		ASSG C 0 A_AlertMonsters
		ASSG C 0 A_StartSound("astroshotgun/fire", CHAN_WEAPON)
		ASSG C 0 A_TakeInventory("AstroShotgunLoaded",1,TIF_NOTAKEINFINITE)
		ASSG C 0 A_FireProjectile("AstroTracerPlayer",-4.5,0)
		ASSG C 0 A_FireProjectile("AstroTracerPlayer",-3.0,0)
		ASSG C 0 A_FireProjectile("AstroTracerPlayer",-1.5,0)
		ASSG C 0 A_FireProjectile("AstroTracerPlayer",0.0,0)
		ASSG C 0 A_FireProjectile("AstroTracerPlayer",1.5,0)
		ASSG C 0 A_FireProjectile("AstroTracerPlayer",3.0,0)
		ASSG C 3 A_FireProjectile("AstroTracerPlayer",4.5,0)
		ASSG D 3
		ASSG EF 5
		ASSG B 17
		ASSG B 0 A_ReFire
		Goto Ready
	AltFire:
		ASSG B 0 A_JumpIfInventory("AstroShotgunLoaded",1,1)
		Goto Dryfire
		ASSG C 0 A_AlertMonsters
		ASSG C 0 A_StartSound("astroshotgun/fire", CHAN_WEAPON)
		ASSG C 0 A_TakeInventory("AstroShotgunLoaded",1,TIF_NOTAKEINFINITE)
		ASSG C 0 A_FireProjectile("AstroTracerPlayer",0,0,0,0,0,1.5)
		ASSG C 0 A_FireProjectile("AstroTracerPlayer",0,0,0,0,0,3.0)
		ASSG C 0 A_FireProjectile("AstroTracerPlayer",0,0,0,0,0,4.5)
		ASSG C 0 A_FireProjectile("AstroTracerPlayer",0,0,0,0,0,0.0)
		ASSG C 0 A_FireProjectile("AstroTracerPlayer",0,0,0,0,0,-1.5)
		ASSG C 0 A_FireProjectile("AstroTracerPlayer",0,0,0,0,0,-4.5)
		ASSG C 3 A_FireProjectile("AstroTracerPlayer",0,0,0,0,0,-3.0)
		ASSG D 3
		ASSG EF 5
		ASSG B 17
		ASSG B 0 A_ReFire
		Goto Ready
	Reload:
		TNT1 A 0 A_JumpIfInventory("AstroShotgunShell",1,2)
		ASSG B 1 Offset(0,35) A_StartSound("weapon/engdry", CHAN_WEAPON)	//replace this sound?
		Goto Ready
		ASSG G 1 Offset(0,35) A_StartSound("weapons/engload3", CHAN_WEAPON)	//replace this sound?
		ASSG G 1 Offset(0,38)
		ASSG G 1 Offset(0,44)
		ASSG G 1 Offset(0,52)
		ASSG G 1 Offset(-1,54)
		ASSG G 1 Offset(-2,56)
		ASSG G 1 Offset(-3,58)
		ASSG G 1 Offset(-4,58)
		ASSG G 1 Offset(-4,57)
		ASSG G 1 Offset(-3,54)
		ASSG G 1 Offset(-3,51)
		ASSG G 1 Offset(-3,53)
		ASSG G 1 Offset(-3,55)
		ASSG G 1 Offset(-3,56)
	ReloadLoop:
		TNT1 A 0 A_TakeInventory("AstroShotgunShell",1,TIF_NOTAKEINFINITE)
		TNT1 A 0 A_GiveInventory("AstroShotgunLoaded")
		TNT1 A 0 A_JumpIfInventory("AstroShotgunLoaded",0,"ReloadFinish")
		TNT1 A 0 A_JumpIfInventory("AstroShotgunShell",1,"ReloadLoop")
	ReloadFinish:
		ASSG G 1 Offset(-3,57)
		ASSG G 1 Offset(-3,59)
		ASSG G 1 Offset(-3,63)
		ASSG G 1 Offset(-3,67)
		ASSG G 1 Offset(-3,65)
		ASSG G 1 Offset(-3,62)
		ASSG G 1 Offset(-3,58)
		ASSG G 1 Offset(-3,55)
		ASSG G 1 Offset(-2,53)
		ASSG G 1 Offset(-2,49)
		ASSG G 1 Offset(-2,46)
		ASSG G 1 Offset(-2,45)
		ASSG G 1 Offset(-1,44)
		ASSG H 1 Offset(-1,46)
		ASSG H 1 Offset(-1,47)
		ASSG I 1 Offset(0,49) A_WeaponReady(WRF_NOBOB)
		ASSG I 1 Offset(0,44) A_WeaponReady(WRF_NOBOB)
		ASSG I 1 Offset(0,38) A_WeaponReady(WRF_NOBOB)
		ASSG I 1 Offset(0,35) A_WeaponReady(WRF_NOBOB)
		ASSG B 1 Offset(0,32) A_WeaponReady(WRF_NOBOB)
		Goto Ready
	Spawn:
		ASSG A -1
		Stop
	}
}

ACTOR AstroShotgunLoaded : Ammo
{
	Tag "$TAGPROTC"
	+IGNORESKILL
	Inventory.MaxAmount 8
	Inventory.Icon "AST101"
}

ACTOR AstroShotgunShell : Ammo
{
	//$Category Astrostein (BoA)/Ammo
	//$Title Protonenflinte (ammo, +5)
	//$Color 6
	Scale 0.50
	Tag "$TAGPROTC"
	Inventory.PickupMessage "$AFLINTE"
	Inventory.Amount 5
	Inventory.MaxAmount 100
	Inventory.Icon "AST101"
	Ammo.BackpackAmount 10
	Ammo.BackpackMaxAmount 100
	States
	{
	Spawn:
		ASSL A -1 LIGHT(ASTRAMM1)
		Stop
	}
}

ACTOR AstroRocketlauncher : NaziAstroWeapon
{
	//$Title (5) Astrostein RocketLauncher
	Weapon.SelectionOrder 1400
	Weapon.AmmoType "AstroRocketLauncherLoaded"
	Weapon.AmmoUse 1
	Weapon.AmmoType2 "AstroRocketAmmo"
	Weapon.AmmoUse2 1
	Weapon.AmmoGive2 5
	Weapon.UpSound "weapons/rezup" //mxd
	Inventory.PickupMessage "$ENERGIE"
	Obituary "$OBENERGIE"
	Tag "$TAGENB95"
	+WEAPON.NOAUTOFIRE
	States
	{
	Select:
		ASRL B 0 A_Raise
		ASRL B 1 A_Raise
		Loop
	Deselect:
		ASRL B 0 A_Lower
		ASRL B 1 A_Lower
		Loop
	Ready:
		ASRL B 0 A_JumpIfInventory("AstroRocketLauncherLoaded",0,2)
		ASRL B 0 A_JumpIfInventory("AstroRocketAmmo",1,2)
		ASRL B 1 A_WeaponReady
		Loop
		ASRL B 1 A_WeaponReady(WRF_ALLOWRELOAD)
		Loop
	Fire:
		ASRL B 0 A_JumpIfInventory("AstroRocketLauncherLoaded",1,1)
		Goto Dryfire
		ASRL CD 4
		ASRL E 0 A_AlertMonsters
		ASRL E 0 A_TakeInventory("AstroRocketLauncherLoaded",1,TIF_NOTAKEINFINITE)
		ASRL E 4 A_FireProjectile("AstroRocket", 0, 0)
		ASRL D 4
		ASRL C 21
		ASRL C 0 A_ReFire
		Goto Ready
	AltFire:
		ASRL B 0 A_JumpIfInventory("AstroRocketLauncherLoaded",1,1)
		Goto Dryfire
		ASRL CD 4
		ASRL E 0 A_AlertMonsters
		ASRL E 0 A_TakeInventory("AstroRocketLauncherLoaded",1,TIF_NOTAKEINFINITE)
		ASRL E 4 A_FireProjectile("AstroRocket_Slow", 0, 0)
		ASRL D 4
		ASRL C 30
		ASRL C 0 A_ReFire
		Goto Ready
	Reload:
		TNT1 A 0 A_JumpIfInventory("AstroRocketAmmo",1,2)
		ASRL F 1 Offset(0,35) A_StartSound("weapon/engdry", CHAN_WEAPON)	//replace this sound?
		Goto Ready
		ASRL F 1 Offset(0,35) A_StartSound("weapons/engload2", CHAN_WEAPON)
		ASRL F 1 Offset(0,38)
		ASRL F 1 Offset(0,44)
		ASRL F 1 Offset(0,52)
		ASRL F 1 Offset(-1,54)
		ASRL F 1 Offset(-2,56)
		ASRL F 1 Offset(-3,58)
		ASRL F 1 Offset(-4,58)
		ASRL F 1 Offset(-4,57)
		ASRL F 1 Offset(-3,54)
		ASRL F 1 Offset(-3,51)
		ASRL F 1 Offset(-3,53)
		ASRL F 1 Offset(-3,55)
		ASRL F 1 Offset(-3,56)
	ReloadLoop:
		TNT1 A 0 A_TakeInventory("AstroRocketAmmo",1,TIF_NOTAKEINFINITE)
		TNT1 A 0 A_GiveInventory("AstroRocketLauncherLoaded")
		TNT1 A 0 A_JumpIfInventory("AstroRocketLauncherLoaded",0,"ReloadFinish")
		TNT1 A 0 A_JumpIfInventory("AstroRocketAmmo",1,"ReloadLoop")
	ReloadFinish:
		ASRL C 1 Offset(-3,57)
		ASRL C 1 Offset(-3,59)
		ASRL C 1 Offset(-3,63)
		ASRL C 1 Offset(-3,67)
		ASRL C 1 Offset(-3,65)
		ASRL C 1 Offset(-3,62)
		ASRL C 1 Offset(-3,58)
		ASRL C 1 Offset(-3,55)
		ASRL C 1 Offset(-2,53)
		ASRL C 1 Offset(-2,49)
		ASRL C 1 Offset(-2,46)
		ASRL C 1 Offset(-2,45)
		ASRL C 1 Offset(-1,44)
		ASRL C 1 Offset(-1,46)
		ASRL C 1 Offset(-1,47)
		ASRL B 1 Offset(0,49) A_WeaponReady(WRF_NOBOB)
		ASRL B 1 Offset(0,44) A_WeaponReady(WRF_NOBOB)
		ASRL B 1 Offset(0,38) A_WeaponReady(WRF_NOBOB)
		ASRL B 1 Offset(0,35) A_WeaponReady(WRF_NOBOB)
		ASRL B 1 Offset(0,32) A_WeaponReady(WRF_NOBOB)
		Goto Ready
	Spawn:
		ASRL A -1 LIGHT(ASTRWEP1)
		Stop
	}
}

ACTOR AstroRocket_Slow : AstroRocket
{
	Speed 4
	Damage 30
	Scale 1.5
	+NOGRAVITY
	BounceType "None"
}

ACTOR AstroRocketLauncherLoaded : Ammo
{
	Tag "$TAGENUNT"
	+IGNORESKILL
	Inventory.MaxAmount 5
	Inventory.Icon "AST301"
}

ACTOR AstroRocketAmmo : Ammo
{
	//$Category Astrostein (BoA)/Ammo
	//$Title Energiegewehr (ammo, +50)
	//$Color 6
	Tag "$TAGENUNT"
	Inventory.PickupMessage "$AENERGY"
	Inventory.MaxAmount 50
	Inventory.Icon "AST301"
	Ammo.BackpackAmount 5
	Ammo.BackpackMaxAmount 100
	States
	{
	Spawn:
		ASRK A -1 LIGHT(ASTRAMM1)
		Stop
	}
}

ACTOR AstroChaingun : NaziAstroWeapon
{
	//$Title (4) Astrostein Chaingun
	Weapon.SelectionOrder 700
	Weapon.AmmoType "AstroChaingunLoaded"
	Weapon.AmmoUse 1
	Weapon.AmmoType2 "AstroClipAmmo"
	Weapon.AmmoUse2 1
	Weapon.AmmoGive2 20
	Weapon.UpSound "weapons/rezup" //mxd
	Inventory.PickupMessage "$REPETIE"
	Obituary "$OBREPETIE"
	Tag "$TAGREPLZ"
	States
	{
	Select:
		ASCG C 0 A_Raise
		ASCG C 1 A_Raise
		Loop
	Deselect:
		ASCG B 0 A_Lower
		ASCG B 1 A_Lower
		Loop
	Ready:
		ASCG B 0 A_JumpIfInventory("AstroChaingunLoaded",0,2)
		ASCG B 0 A_JumpIfInventory("AstroClipAmmo",1,2)
		ASCG B 1 A_WeaponReady
		Loop
		ASCG B 1 A_WeaponReady(WRF_ALLOWRELOAD)
		Loop
	Fire:
		ASCG B 0 A_JumpIfInventory("AstroChaingunLoaded",1,1)
		Goto Dryfire
		ASCG D 0 A_AlertMonsters
		ASCG D 0 A_StartSound("astrochaingun/fire", CHAN_WEAPON)
		ASCG D 0 A_TakeInventory("AstroChaingunLoaded",1,TIF_NOTAKEINFINITE)
		ASCG D 4 A_FireProjectile("AstroTracerPlayer",frandom(-1.0,1.0),0,0,0,0,frandom(-1.0,1.0))
		ASCG D 0 A_JumpIfInventory("AstroChaingunLoaded",1,1)
		Goto Ready
		ASCG E 0 A_TakeInventory("AstroChaingunLoaded",1,TIF_NOTAKEINFINITE)
		ASCG E 4 A_FireProjectile("AstroTracerPlayer",frandom(-1.0,1.0),0,0,0,0,frandom(-1.0,1.0))
		ASCG D 0 A_ReFire
		Goto Ready
	AltFire:
		ASCG B 0 A_JumpIfInventory("AstroChaingunLoaded",1,1)
		Goto Dryfire
		ASCG D 0 A_AlertMonsters
		ASCG D 0 A_StartSound("astrochaingun/fire", CHAN_WEAPON)
		ASCG D 0 A_TakeInventory("AstroChaingunLoaded",1,TIF_NOTAKEINFINITE)
		ASCG D 6 A_FireProjectile("AstroTracerPlayer",frandom(-0.2,0.2),0,0,0,0,frandom(-0.2,0.2))
		ASCG D 0 A_JumpIfInventory("AstroChaingunLoaded",1,1)
		Goto Ready
		ASCG E 0 A_TakeInventory("AstroChaingunLoaded",1,TIF_NOTAKEINFINITE)
		ASCG E 6 A_FireProjectile("AstroTracerPlayer",frandom(-0.2,0.2),0,0,0,0,frandom(-0.2,0.2))
		ASCG D 0 A_ReFire
		Goto Ready
	Reload:
		TNT1 A 0 A_JumpIfInventory("AstroClipAmmo",1,2)
		ASCG B 1 Offset(0,35) A_StartSound("weapon/engdry", CHAN_WEAPON)	//replace this sound?
		Goto Ready
		ASCG B 1 Offset(0,35) A_StartSound("weapons/engload", CHAN_WEAPON)	//replace this sound?
		ASCG B 1 Offset(0,38)
		ASCG B 1 Offset(0,44)
		ASCG B 1 Offset(0,52)
		ASCG B 1 Offset(-1,54)
		ASCG B 1 Offset(-2,56)
		ASCG B 1 Offset(-3,58)
		ASCG B 1 Offset(-4,58)
		ASCG G 1 Offset(-4,57)
		ASCG G 1 Offset(-3,54)
		ASCG G 1 Offset(-3,51)
		ASCG G 1 Offset(-3,53)
		ASCG G 1 Offset(-3,55)
		ASCG G 15 Offset(-3,56)
	ReloadLoop:
		TNT1 A 0 A_TakeInventory("AstroClipAmmo",1,TIF_NOTAKEINFINITE)
		TNT1 A 0 A_GiveInventory("AstroChaingunLoaded")
		TNT1 A 0 A_JumpIfInventory("AstroChaingunLoaded",0,"ReloadFinish")
		TNT1 A 0 A_JumpIfInventory("AstroClipAmmo",1,"ReloadLoop")
	ReloadFinish:
		ASCG G 1 Offset(-3,57)
		ASCG G 1 Offset(-3,59)
		ASCG G 1 Offset(-3,63)
		ASCG G 1 Offset(-3,67)
		ASCG G 1 Offset(-3,65)
		ASCG G 1 Offset(-3,62)
		ASCG G 1 Offset(-3,58)
		ASCG G 1 Offset(-3,55)
		ASCG F 1 Offset(-2,53)
		ASCG F 1 Offset(-2,49)
		ASCG F 1 Offset(-2,46)
		ASCG F 1 Offset(-2,45)
		ASCG F 1 Offset(-1,44)
		ASCG F 1 Offset(-1,46)
		ASCG F 1 Offset(-1,47)
		ASCG B 1 Offset(0,49) A_WeaponReady(WRF_NOBOB)
		ASCG B 1 Offset(0,44) A_WeaponReady(WRF_NOBOB)
		ASCG B 1 Offset(0,38) A_WeaponReady(WRF_NOBOB)
		ASCG B 1 Offset(0,35) A_WeaponReady(WRF_NOBOB)
		ASCG B 1 Offset(0,32) A_WeaponReady(WRF_NOBOB)
		Goto Ready
	Spawn:
		ASCG A -1 LIGHT(ASTRWEP2)
		Stop
	}
}

ACTOR AstroChaingunLoaded : Ammo
{
	Tag "$TAGLAZRD"
	+IGNORESKILL
	Inventory.MaxAmount 30
	Inventory.Icon "AST201"
}

ACTOR AstroClipAmmo : Ammo
{
	//$Category Astrostein (BoA)/Ammo
	//$Title Repetierlaser (ammo, +10)
	//$Color 6
	Scale 0.50
	Tag "$TAGLAZRD"
	Inventory.PickupMessage "$ALASER"
	Inventory.Amount 10
	Inventory.MaxAmount 200
	Inventory.Icon "AST201"
	Ammo.BackpackAmount 20
	Ammo.BackpackMaxAmount 200
	States
	{
	Spawn:
		ASCL A -1 LIGHT(ASTRAMM3)
		Stop
	}
}

///////////////////////////////////////////////////////////
///// ASTRO SHOCKER ///////////////////////////////////////
///////////////////////////////////////////////////////////

ACTOR AstrosteinMelee : NaziAstroWeapon
{
	//$Title (1) Astrostein Melee
	Tag "$TAGSHOKR"
	Scale 1.0
	Weapon.Kickback 0
	Weapon.SelectionOrder 2200
	Weapon.ReadySound "weapons/rezidle"
	Inventory.PickupMessage "$ELEKTRO"
	Obituary "$OBELEKTRO"
	-WEAPON.AMMO_CHECKBOTH
	-WEAPON.NOALERT
	+WEAPON.MELEEWEAPON
	+WEAPON.AMMO_OPTIONAL
	States
	{
	Select:
		REZG C 0 A_StartSound("weapons/rezup", CHAN_BODY, 0, 1.0) //mxd. UpSound will be cut off by ReadySound
	SelectLoop:
		REZG C 0 A_Raise
		REZG C 1 A_Raise
		Loop
	Deselect:
		REZG C 0 A_Lower
		REZG C 1 A_Lower
		Loop
	Ready:
		REZG CD 4 A_WeaponReady
		Loop
	Fire:
		REZG AB 3 A_Saw("weapons/rezfull","weapons/rezhit",2,"ElektroshockerPuff")
		REZG B 0 A_ReFire
		Goto Ready
	Spawn:
		REZN A -1
		Stop
	}
}

ACTOR ElektroshockerPuff : BulletPuff
{
	+BLOODLESSIMPACT
	+PUFFONACTORS
	DamageType "Reznator"
	RenderStyle "Add"
	Alpha 0.8
	VSpeed 0
	DamageType "Melee"
	States
	{
	Spawn:
	Melee:
		TNT1 AAAAA 0 A_SpawnItemEx("SparkG", 0, 0, 0, random(1,2), random(1,2), random(1,2), random(1,360), SXF_CLIENTSIDE)
		TNT1 A 0 A_SpawnItemEx("SparkFlareG")
		RZAP ACEGH 2 BRIGHT
		Stop
	}
}

///////////////////////////////////////////////////////////////////
/////// ASTROSTEIN PISTOL /////////////////////////////////////////
///////////////////////////////////////////////////////////////////


ACTOR AstroPistolLoaded : Ammo
{
	Tag "$TAGLAZRD"
	+IGNORESKILL
	Inventory.MaxAmount 10
	Inventory.Icon "AST201"
}

ACTOR AstroLuger : NaziAstroWeapon //quite the same code from Luger P08 - ozy81
{
	//$Title (2) Astrostein Luger
	Tag "Astro Luger"
	Scale 1.0
	Weapon.SelectionOrder 200
	Weapon.AmmoType "AstroPistolLoaded"
	Weapon.AmmoUse 1
	Weapon.AmmoType2 "AstroClipAmmo"
	Weapon.AmmoUse2 1
	Weapon.AmmoGive2 8
	Weapon.UpSound "luger/select"
	+WEAPON.NOAUTOFIRE
	Obituary "$OBALUGER"
	Inventory.PickupMessage "$ALUGER"
	States
	{
	Ready:
		PLPI A 0 A_JumpIfInventory("AstroPistolLoaded",0,2)
		PLPI A 0 A_JumpIfInventory("AstroClipAmmo",1,2)
		PLPI A 1 A_WeaponReady
		Loop
		PLPI A 1 A_WeaponReady(WRF_ALLOWRELOAD)
		Loop
	Deselect:
		PLPI A 0 A_Lower
		PLPI A 1 A_Lower
		Loop
	Select:
		PLPI A 0 A_Raise
		PLPI A 1 A_Raise
		Loop
	Fire:
		PLPI A 0 A_JumpIfInventory("AstroPistolLoaded",1,1)
		Goto Dryfire
		PLPI A 0 A_GunFlash
		PLPI A 0 A_SetPitch(pitch-(0.2*CallACS("boa_recoilamount")))
		PLPI A 0 A_JumpIf(waterlevel > 0,2)
		PLPI A 0 A_StartSound("astrochaingun/fire", CHAN_WEAPON)
		PLPI A 0 A_AlertMonsters
		PLPI A 0 A_TakeInventory("AstroPistolLoaded",1,TIF_NOTAKEINFINITE)
		PLPI B 1 A_FireProjectile("AstroTracerPlayer",0,0,0,0,0,frandom(-0.2,0.2))
		PLPI B 1
		PLPI B 1 Offset(0,36) A_SetPitch(pitch-(0.2*CallACS("boa_recoilamount")))
		PLPI B 1 Offset(0,41) A_CheckReload
		PLPI C 1 Offset(0,35) A_WeaponReady(WRF_NOBOB)
		PLPI C 1 Offset(0,33) A_WeaponReady(WRF_NOBOB)
		PLPI C 1 Offset(0,31) A_WeaponReady(WRF_NOBOB)
		PLPI A 1 Offset(0,32) A_WeaponReady(WRF_NOBOB)
		Goto Ready
	Reload:
		PLPI A 1 Offset(0,35) A_StartSound("weapons/engload4", CHAN_5)
		PLPI A 1 Offset(0,38)
		PLPI D 1 Offset(0,44)
		PLPI D 1 Offset(0,52)
		PLPI D 1 Offset(0,62)
		PLPI D 1 Offset(0,72)
		PLPI D 1 Offset(0,82)
		TNT1 A 11
	ReloadLoop:
		TNT1 A 0 A_TakeInventory("AstroClipAmmo",1,TIF_NOTAKEINFINITE)
		TNT1 A 0 A_GiveInventory("AstroPistolLoaded")
		TNT1 A 0 A_JumpIfInventory("AstroPistolLoaded",0,"ReloadFinish")
		TNT1 A 0 A_JumpIfInventory("AstroClipAmmo",1,"ReloadLoop")
	ReloadFinish:
		PLPI D 1 Offset(0,82)
		PLPI D 1 Offset(0,72)
		PLPI D 1 Offset(0,62)
		PLPI A 1 Offset(0,52)
		PLPI A 1 Offset(0,44)
		PLPI A 1 Offset(0,38)
		PLPI A 1 Offset(0,35)
		PLPI A 1 Offset(0,32)
		Goto Ready
	Flash:
		PLPI A 1 BRIGHT A_Light2
		TNT1 A 1
		TNT1 A 2 A_Light1
		Goto LightDone
	Spawn:
		PLSP A -1
		Stop
	}
}

///////////////////////////////////////////////////////////////////////////////
/////// ASTROSTEIN HANDGRANATE ////////////////////////////////////////////////
///////////////////////////////////////////////////////////////////////////////
// See scripts/actors/weapons/grenade.txt for AstroGrenadePickup
ACTOR AstroGrenadeToken : ParallelInventory { ParallelInventory.Type "GrenadePickup" }

ACTOR AstroHandGrenade : HandGrenade
{
	BounceFactor 0.4
	WallBounceFactor 0.8
	Scale 0.25
	States
	{
	Spawn:
		ASGR A 2 A_Countdown
		ASGR ACDEFGH 2
		Loop
	Death:
		ASGR I 35 A_SkipIfHit
		TNT1 A 0 A_SpawnGroundSplash
		TNT1 A 0 A_AlertMonsters
		TNT1 A 0 A_SetScale(1.75,1.75)
		TNT1 A 0 A_SetTranslucent(0.75,1)
		TNT1 A 0 A_StartSound("astrostein/explosion", CHAN_AUTO, 0, 1.0, 0.1)
		TNT1 A 1 A_SpawnItemEx("AstrosteinExplosion_Medium")
		TNT1 A 1 Radius_Quake(10,10,0,16,0)
		Stop
	}
}