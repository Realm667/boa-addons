ACTOR MP40 : NaziWeapon
{
	//$Category Weapons (BoA)
	//$Title (4) MP40
	//$Color 14
	Scale .5
	Weapon.SelectionOrder 600
	Weapon.AmmoType "MP40Loaded"
	Weapon.AmmoUse 1
	Weapon.AmmoType2 "9mmAmmo"
	Weapon.AmmoUse2 1
	Weapon.UpSound "mp40/select"
	Tag "MP 40"
	Inventory.PickupMessage "$MP40"
	Weapon.AmmoGive2 32
	States
	{
	Ready:
		RIFG A 0 A_JumpIfInventory("MP40Loaded",0,2)
		RIFG A 0 A_JumpIfInventory("9mmAmmo",1,2)
		RIFG A 1 A_WeaponReady
		Loop
		RIFG A 1 A_WeaponReady(WRF_ALLOWRELOAD)
		Loop
	Deselect:
		RIFG A 0 A_Lower
		RIFG A 1 A_Lower
		Loop
	Select:
		RIFG A 0 A_Raise
		RIFG A 1 A_Raise
		Loop
	Fire:
		RIFG A 0 A_JumpIfInventory("MP40Loaded",1,1)
		Goto Dryfire
		RIFG A 0 A_GunFlash
		RIFG A 0 A_SetPitch(pitch-(0.5*CallACS("boa_recoilamount")))
		RIFG A 0 A_JumpIf(waterlevel > 0,2)
		RIFG A 0 A_FireProjectile("ChainSmokeSpawner",0,0,0,random(-4,4),0,0)
		RIFG A 0 A_StartSound("mp40/fire", CHAN_WEAPON, 0, frandom(0.6, 0.8))
		RIFG A 0 A_SpawnItemEx("9MMCasing",12,-20,32,8,random(-2,2),random(0,4),random(-55,-80),SXF_NOCHECKPOSITION)
		RIFG A 0 A_AlertMonsters
		RIFG A 1 A_FireProjectile("MP40Tracer",frandom(-1.4,1.4))
		RIFG B 1 Offset(0,38)
		RIFG B 1 Offset(0,44)
		RIFG B 1 Offset(0,40)
		RIFG A 0 A_JumpIfInventory("MP40Seq",1,"Fire2")
		TNT1 A 0 A_GiveInventory("MP40Seq")
		TNT1 A 0 A_CheckReload
		Goto Ready
	Fire2:
		RIFG A 1 Offset(0,36) A_TakeInventory("MP40Seq")
		TNT1 A 0 A_CheckReload
		Goto Ready
	Reload:
		RIFG A 1 Offset(0,35) A_StartSound("mp40/reload", CHAN_5, 0, frandom(0.6, 0.8))
		RIFG A 1 Offset(0,38)
		RIFG A 1 Offset(0,44)
		RIFG C 1 Offset(0,52)
		RIFG C 1 Offset(-1,54)
		RIFG C 1 Offset(-2,56)
		RIFG C 1 Offset(-3,58)
		RIFG C 1 Offset(-4,58)
		RIFG C 1 Offset(-4,57)
		RIFG C 1 Offset(-3,54)
		RIFG C 1 Offset(-3,51)
		RIFG C 1 Offset(-3,53)
		RIFG C 1 Offset(-3,55)
		RIFG C 15 Offset(-3,56)
	ReloadLoop:
		TNT1 A 0 A_TakeInventory("9mmAmmo",1,TIF_NOTAKEINFINITE)
		TNT1 A 0 A_GiveInventory("MP40Loaded")
		TNT1 A 0 A_JumpIfInventory("MP40Loaded",0,"ReloadFinish")
		TNT1 A 0 A_JumpIfInventory("9mmAmmo",1,"ReloadLoop")
	ReloadFinish:
		RIFG D 1 Offset(-3,57)
		RIFG D 1 Offset(-3,59)
		RIFG D 1 Offset(-3,63)
		RIFG D 1 Offset(-3,67)
		RIFG D 1 Offset(-3,65)
		RIFG D 1 Offset(-3,62)
		RIFG B 1 Offset(-3,58)
		RIFG A 1 Offset(-3,55)
		RIFG A 1 Offset(-2,53)
		RIFG A 1 Offset(-2,49)
		RIFG A 1 Offset(-2,46)
		RIFG A 1 Offset(-2,45)
		RIFG A 1 Offset(-1,44)
		RIFG A 1 Offset(-1,46)
		RIFG A 1 Offset(-1,47)
		RIFG A 1 Offset(0,49) A_WeaponReady(WRF_NOBOB)
		RIFG A 1 Offset(0,44) A_WeaponReady(WRF_NOBOB)
		RIFG A 1 Offset(0,38) A_WeaponReady(WRF_NOBOB)
		RIFG A 1 Offset(0,35) A_WeaponReady(WRF_NOBOB)
		RIFG A 1 Offset(0,32) A_WeaponReady(WRF_NOBOB)
		Goto Ready
	Flash:
		RIFF A 1 A_Light2
		TNT1 A 1
		TNT1 A 2 A_Light1
		Goto LightDone
	Spawn:
		MP40 A -1
		Stop
	}
}

ACTOR MP40Loaded : Ammo
{
	Tag "9x19mm"
	+IGNORESKILL
	Inventory.MaxAmount 32
	Inventory.Icon WALT01
}

ACTOR MP40Seq : Inventory{}