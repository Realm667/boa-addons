// I don't actually just modelshot the models as is, if you're wondering whether or not doing the screenshots are easy. Often than not I have to pull up my old Goldsource tools and trickery to modify meshes (mainly remove hands/sleeves), Photoshop the textures directly to maybe add some baked lights so it won't look as dull, alter UV mapping so the gun didn't look like its entire skin had shifted to one particular side, or even hack the meshes from the reference SMDs over to a different rig because the initial one had the camera origin in such a way where I can't even center the bloody thing. This one in particular had all of them needed to be done before I can take 1-2 pictures of it to use for the weapon. Quite a long winded one.

ACTOR T100 : NaziWeapon
{
	//$Category Weapons (BoA)
	//$Title (4) MP18
	//$Color 14
	Scale 0.30
	Weapon.SlotNumber 4
	Weapon.SelectionOrder 600
	Inventory.PickupMessage "You got the Type 100!"
	Weapon.AmmoType "Type100Loaded"
	Weapon.AmmoUse 1
	Weapon.AmmoType2 "NambuAmmo"
	Weapon.AmmoUse2 1
	Weapon.AmmoGive1 30
	Weapon.AmmoGive2 60
	Weapon.UpSound "t100/select"
	Tag "Type 100"
	States
	{
	Ready:
		TY10 A 0 A_JumpIfInventory("Type100Loaded",0,2)
		TY10 A 0 A_JumpIfInventory("NambuAmmo",1,2)
		TY10 A 1 A_WeaponReady
		Loop
		TY10 A 1 A_WeaponReady(WRF_ALLOWRELOAD)
		Loop
	Deselect:
		TY10 A 0 A_Lower
		TY10 A 1 A_Lower
		Loop
	Select:
		TY10 A 0 A_Raise
		TY10 A 1 A_Raise
		Loop
	Fire:
		TY10 A 0 A_JumpIfInventory("Type100Loaded",1,1)
		Goto Dryfire
		TY10 A 0 A_GunFlash
		TNT1 A 0 
		TY10 B 0 A_PlaySound("t100/fire",CHAN_WEAPON)
		TY10 B 0 A_SpawnItemEx("NambuCasing",12,-20,32,8,random(-2,2),random(0,4),random(-55,-80),SXF_NOCHECKPOSITION)
		TY10 B 0 A_AlertMonsters
		TY10 B 1 A_FireCustomMissile("NambuTracer",frandom(-1.0,1.0),1,0,0,0,frandom(-1.0,1.0))
		TY10 B 1 Offset(0,38) A_SetPitch(pitch-(0.5*boa_recoilamount))
		TY10 B 0 A_JumpIf(waterlevel > 0,2)
		TY10 B 0 A_FireProjectile("ChainSmokeSpawner",0,0,0,random(-4,4),0,0)
		TY10 A 1 Offset(0,37) 
		TY10 A 1 Offset(0,35) A_ReFire
		TY10 A 1 Offset(0,33)
		TNT1 A 0 A_CheckReload
		Goto Ready
	Reload:
		TY10 A 1 Offset(0,38) A_StartSound("t100/reload", CHAN_5)
		TY10 A 1 Offset(0,44)
		TY10 A 1 Offset(0,56)
		TY10 A 1 Offset(-2,60)
		TY10 A 1 Offset(-4,72)
		TY10 A 1 Offset(-7,80)
		TY10 C 1 Offset(-10,82)
		TY10 C 20 Offset(-11,84)
	ReloadLoop:
		TNT1 A 0 A_TakeInventory("NambuAmmo",1,TIF_NOTAKEINFINITE)
		TNT1 A 0 A_GiveInventory("Type100Loaded")
		TNT1 A 0 A_JumpIfInventory("Type100Loaded",0,"ReloadFinish")
		TNT1 A 0 A_JumpIfInventory("NambuAmmo",1,"ReloadLoop")
	ReloadFinish:
		TY10 D 1 Offset(-10,84)
		TY10 D 1 Offset(-10,90)
		TY10 D 1 Offset(-10,98)
		TY10 D 1 Offset(-10,110)
		TY10 A 1 Offset(-10,98)
		TY10 A 1 Offset(-10,88)
		TY10 A 1 Offset(-10,78)
		TY10 A 1 Offset(-10,74)
		TY10 A 1 Offset(-10,70)
		TY10 A 1 Offset(-9,68)
		TY10 A 1 Offset(-8,60)
		TY10 A 1 Offset(-7,54)
		TY10 A 1 Offset(-5,48)
		TY10 A 1 Offset(-3,42)
		TY10 A 1 Offset(-1,36)
		TY10 A 1 Offset(0,32)
		Goto Ready
	Flash:
		T10F A 1 BRIGHT A_Light2
		T10F B 1 BRIGHT
		TNT1 A 2 A_Light1
		Goto LightDone
	Spawn:
		TY10 Z -1
		Stop
	}
}

ACTOR Type100Loaded : Ammo
{
	Tag "8x22mm Nambu"
	Inventory.MaxAmount 30
	+IGNORESKILL
	Inventory.Icon NAMA03
}