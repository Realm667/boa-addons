// I actually have an MP34 model lying around here as well, though I ended up just putting the lone MP18 in 'cause outside of the snail mag, the difference between the two isn't as obvious. Yeah, I know the internals are all different, but they're both an SMG loaded in 9mm with a 32 round mag and wooden guard plus stock. Sorry, MP34 fans.

ACTOR MP18 : NaziWeapon
{
	//$Category Weapons (BoA)
	//$Title (4) MP18
	//$Color 14
	Scale 0.30
	Weapon.SlotNumber 4
	Weapon.SelectionOrder 600
	Inventory.PickupMessage "You got the Bergmann MP 18!"
	Weapon.AmmoType "MP18Loaded"
	Weapon.AmmoUse 1
	Weapon.AmmoType2 "Ammo9mm"
	Weapon.AmmoUse2 1
	Weapon.AmmoGive1 32
	Weapon.AmmoGive2 64
	Weapon.UpSound "mp18/select"
	Tag "MP 18"
	States
	{
	Ready:
		MP18 A 0 A_JumpIfInventory("MP18Loaded",0,2)
		MP18 A 0 A_JumpIfInventory("Ammo9mm",1,2)
		MP18 A 1 A_WeaponReady
		Loop
		MP18 A 1 A_WeaponReady(WRF_ALLOWRELOAD)
		Loop
	Deselect:
		MP18 A 0 A_Lower
		MP18 A 1 A_Lower
		Loop
	Select:
		MP18 A 0 A_Raise
		MP18 A 1 A_Raise
		Loop
	Fire:
		MP18 A 0 A_JumpIfInventory("MP18Loaded",1,1)
		Goto Dryfire
		MP18 A 0 A_GunFlash
		TNT1 A 0 
		MP18 B 0 A_PlaySound("mp18/fire",CHAN_WEAPON)
		MP18 B 0 A_SpawnItemEx("Casing9mm",12,-20,32,8,random(-2,2),random(0,4),random(-55,-80),SXF_NOCHECKPOSITION)
		MP18 B 0 A_AlertMonsters
		MP18 B 1 A_FireCustomMissile("MP40Tracer",frandom(-1.0,1.0),1,0,0,0,frandom(-1.0,1.0))
		MP18 B 1 Offset(0,38) A_SetPitch(pitch-(0.5*boa_recoilamount))
		MP18 B 0 A_JumpIf(waterlevel > 0,2)
		MP18 B 0 A_FireProjectile("ChainSmokeSpawner",0,0,0,random(-4,4),0,0)
		MP18 B 1 Offset(0,37)
		MP18 A 1 Offset(0,35)
		MP18 A 1 Offset(0,33)
		TNT1 A 0 A_CheckReload
		Goto Ready
	Reload:
		MP18 A 1 Offset(0,38) A_StartSound("mp18/reload", CHAN_5)
		MP18 A 1 Offset(0,44)
		MP18 A 1 Offset(0,56)
		MP18 A 1 Offset(-2,60)
		MP18 A 1 Offset(-4,72)
		MP18 A 1 Offset(-7,80)
		MP18 C 1 Offset(-10,82)
		MP18 C 20 Offset(-11,84)
	ReloadLoop:
		TNT1 A 0 A_TakeInventory("Ammo9mm",1,TIF_NOTAKEINFINITE)
		TNT1 A 0 A_GiveInventory("MP18Loaded")
		TNT1 A 0 A_JumpIfInventory("MP18Loaded",0,"ReloadFinish")
		TNT1 A 0 A_JumpIfInventory("Ammo9mm",1,"ReloadLoop")
	ReloadFinish:
		MP18 D 1 Offset(-10,84)
		MP18 D 1 Offset(-10,90)
		MP18 D 1 Offset(-10,98)
		MP18 D 1 Offset(-10,110)
		MP18 A 1 Offset(-10,98)
		MP18 A 1 Offset(-10,88)
		MP18 A 1 Offset(-10,78)
		MP18 A 1 Offset(-10,74)
		MP18 A 1 Offset(-10,70)
		MP18 A 1 Offset(-9,68)
		MP18 A 1 Offset(-8,60)
		MP18 A 1 Offset(-7,54)
		MP18 A 1 Offset(-5,48)
		MP18 A 1 Offset(-3,42)
		MP18 A 1 Offset(-1,36)
		MP18 A 1 Offset(0,32)
		Goto Ready
	Flash:
		M18F A 1 BRIGHT A_Light2
		M18F B 1 BRIGHT
		TNT1 A 2 A_Light1
		Goto LightDone
	Spawn:
		MP18 Z -1
		Stop
	}
}

ACTOR MP18Loaded : Ammo
{
	Tag "9x19mm"
	Inventory.MaxAmount 32
	+IGNORESKILL
	Inventory.Icon WALT04
}