// There was a point where I thought about adding tact/full reload states for all of the guns during the implementation of this thing, but then I'd have to redo the entire weapons roster all over, not just ConfisWeaps. The thought quickly passes.

ACTOR SVT40 : NaziWeapon
{
	//$Category Weapons (BoA)
	//$Title (5) SVT40
	//$Color 14
	Scale 0.55
	Weapon.SlotNumber 5
	Weapon.SelectionOrder 750
	Weapon.AmmoType "SVT40Loaded"
	Weapon.AmmoUse 1
	Weapon.AmmoType2 "NagantAmmo"
	Weapon.AmmoUse2 1
	Weapon.AmmoGive1 10
	Weapon.AmmoGive2 20
	Inventory.PickupMessage "You got the Tokarev SVT-40!"
	Tag "SVT-40"
	Weapon.UpSound "svt40/select"
	+WEAPON.NOAUTOFIRE
	States
	{
	Select:
		SVTG A 0 A_Raise
		SVTG A 1 A_Raise
		Loop
	Deselect:
		SVTG A 0 A_Lower
		SVTG A 1 A_Lower
		Loop
	Ready:
		SVTG A 0 A_JumpIfInventory("SVT40Loaded",0,2)
		SVTG A 0 A_JumpIfInventory("NagantAmmo",1,2)
		SVTG A 1 A_WeaponReady
		Loop
		SVTG A 1 A_WeaponReady(WRF_ALLOWRELOAD)
		Loop
	Fire:
		SVTG A 0 A_JumpIfInventory("SVT40Loaded",1,1)
		Goto Dryfire
		SVTG A 0 A_StartSound("svt40/fire",CHAN_WEAPON)
		SVTG A 0 A_GunFlash
		SVTG A 0 A_SpawnItemEx("MosinCasing",12,-20,32,8,random(-2,2),random(0,4),random(-55,-80),SXF_NOCHECKPOSITION)
		SVTG A 0 A_AlertMonsters
		SVTG A 1 A_FireProjectile("MosinTracer",frandom(-1.0,1.0),1,0,0,0,frandom(-1.0,1.0))
		SVTG A 0 A_JumpIf(waterlevel > 0,2)
		SVTG A 0 A_FireProjectile("ShotSmokeSpawner",0,0,0,random(-4,4),0,0)
		SVTG A 1 A_SetPitch(pitch-(1.4*boa_recoilamount))
		SVTG CDEDC 1
		TNT1 A 0 A_CheckReload
		SVTG AAFFFA 1 A_WeaponReady(WRF_NOBOB)
		Goto Ready
	Reload:
		SVTG A 1 Offset(0,35) A_StartSound("svt40/reload",CHAN_5)
		SVTG A 1 Offset(-2,38)
		SVTG A 1 Offset(-4,44)
		SVTG A 1 Offset(-6,52)
		SVTG A 1 Offset(-7,57)
		SVTG G 1 Offset(-8,62)
		SVTG G 1 Offset(-9,67)
		SVTG G 1 Offset(-10,72)
		SVTG G 1 Offset(-11,77)
		SVTG G 1 Offset(-11,80)
		SVTG G 1 Offset(-12,82)
		SVTG G 1 Offset(-12,84)
		SVTG G 1 Offset(-13,86)
		SVTG G 1 Offset(-13,87)
		SVTG G 2 Offset(-14,88)
		SVTG G 3 Offset(-14,89)
	ReloadLoop:
		TNT1 A 0 A_TakeInventory("NagantAmmo",1,TIF_NOTAKEINFINITE)
		TNT1 A 0 A_GiveInventory("SVT40Loaded")
		TNT1 A 0 A_JumpIfInventory("SVT40Loaded",0,"ReloadFinish")
		TNT1 A 0 A_JumpIfInventory("NagantAmmo",1,"ReloadLoop")
	ReloadFinish:
		SVTG H 2 Offset(-10,96)
		SVTG H 1 Offset(-6,100)
		SVTG H 1 Offset(-7,90)
		SVTG H 1 Offset(-6,80)
		SVTG H 1 Offset(-4,70)
		SVTG H 1 Offset(-2,60)
		SVTG H 1 Offset(0,50)
		SVTG A 1 Offset(0,40)
		SVTG A 1 Offset(0,32)
		Goto Ready
	Flash:
		SVTF A 1 BRIGHT A_Light2
		SVTF B 1 BRIGHT
		TNT1 A 2 A_Light1
		Goto LightDone
	Spawn:
		SVTP A -1
		Stop
	}
}

ACTOR SVT40Loaded : Ammo
{
	Tag "7.62x54mmR"
	+IGNORESKILL
	Inventory.MaxAmount 10
	Inventory.Icon MSAM02
}