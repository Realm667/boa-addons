// I'm not too sure about this gun at first. Felt a bit too duplicaty. Quite grateful that the T1928 atleast looks different enough from the M1A1. 

ACTOR T1928 : NaziWeapon
{
	//$Category Weapons (BoA Addons)
	//$Title (4) Thompson 1928
	//$Color 14
	Weapon.SlotNumber 4
	Scale .4
	Weapon.SelectionOrder 600
	Weapon.AmmoType "T1928Loaded"
	Weapon.AmmoUse 1
	weapon.ammotype2 "45ACPAmmo"
	weapon.ammouse2 1
	Weapon.AmmoGive1 50
	Weapon.AmmoGive2 100
	Tag "Thompson M1928"
	Inventory.PickupMessage "You got the Thompson M1928!"
	Weapon.UpSound "thompson/select"
	States
	{
	Ready:
		T28G A 0 A_JumpIfInventory("T1928Loaded",0,2)
		T28G A 0 A_JumpIfInventory("45ACPAmmo",1,2)
		T28G A 1 A_WeaponReady
		Loop
		T28G A 1 A_WeaponReady(WRF_ALLOWRELOAD)
		Loop
	Deselect:
		T28G A 0 A_Lower
		T28G A 1 A_Lower
		Loop
	Select:
		T28G A 0 A_Raise
		T28G A 1 A_Raise
		Loop
	Fire:
		T28G A 0 A_JumpIfInventory("T1928Loaded",1,1)
		Goto Dryfire
		T28G A 0 A_GunFlash
		T28G A 0 A_PlaySound("thompson/fire",CHAN_WEAPON)
		T28G A 0 A_SpawnItemEx("45ACPCasing",12,-20,32,8,random(-2,2),random(0,4),random(-55,-80),SXF_NOCHECKPOSITION)
		T28G A 0 A_AlertMonsters
		T28G D 1 A_FireCustomMissile("M1ThompsonTracer",frandom(-1.5,1.5),1,0,0,0,frandom(-1.0,1.0))
		T28G D 1 Offset(0,40) A_SetPitch(pitch-(0.6*boa_recoilamount))
		T28G A 0 A_JumpIf(waterlevel > 0,2)
		T28G A 0 A_FireProjectile("ChainSmokeSpawner",0,0,0,random(-4,4),0,0)
		T28G A 1 Offset(0,34)
		TNT1 A 0 A_Refire
		Goto Ready
	Hold:
		T28G A 0 A_JumpIfInventory("T1928Loaded",1,1)
		Goto Dryfire
		T28G A 0 A_GunFlash
		T28G A 0 A_PlaySound("thompson/fire",CHAN_WEAPON)
		T28G A 0 A_SpawnItemEx("45ACPCasing",12,-20,32,8,random(-2,2),random(0,4),random(-55,-80),SXF_NOCHECKPOSITION)
		T28G A 0 A_AlertMonsters
		T28G D 1 Offset(0,36) A_FireCustomMissile("M1ThompsonTracer",frandom(-2.0,2.0),1,0,0,0,frandom(-2.0,2.0))
		T28G D 1 Offset(0,40) A_SetPitch(pitch-(1.6*boa_recoilamount))
		T28G A 0 A_JumpIf(waterlevel > 0,2)
		T28G A 0 A_FireProjectile("ChainSmokeSpawner",0,0,0,random(-4,4),0,0)
		T28G A 1 Offset(0,34)
		TNT1 A 0 A_Refire
		Goto Ready
	Reload:
		TNT1 A 0 
		T28G E 1 Offset(0,35) A_PlaySound("t1928/reload",5)
		T28G E 1 Offset(0,38)
		T28G E 1 Offset(0,44)
		T28G B 1 Offset(0,52)
		T28G B 1 Offset(-1,54)
		T28G B 1 Offset(-2,56)
		T28G B 1 Offset(-3,58)
		T28G B 1 Offset(-4,58)
		T28G B 1 Offset(-4,57)
		T28G B 1 Offset(-3,54)
		T28G B 1 Offset(-3,51)
		T28G B 1 Offset(-3,53)
		T28G B 1 Offset(-3,55)
		T28G B 1 Offset(-3,56)
	ReloadLoop:
		TNT1 A 0 A_TakeInventory("45ACPAmmo",1,TIF_NOTAKEINFINITE)
		TNT1 A 0 A_GiveInventory("T1928Loaded")
		TNT1 A 0 A_JumpIfInventory("T1928Loaded",0,"ReloadFinish")
		TNT1 A 0 A_JumpIfInventory("45ACPAmmo",1,"ReloadLoop")
	ReloadFinish:
		T28G E 1 Offset(-3,57)
		T28G E 1 Offset(-3,59)
		T28G E 1 Offset(-3,63)
		T28G E 1 Offset(-3,67)
		T28G E 1 Offset(-3,65)
		T28G E 1 Offset(-3,62)
		T28G E 1 Offset(-3,58)
		T28G E 1 Offset(-3,55)
		T28G C 1 Offset(-2,53)
		T28G C 1 Offset(-2,49)
		T28G C 1 Offset(-2,46)
		T28G C 1 Offset(-2,45)
		T28G C 1 Offset(-1,44)
		T28G C 1 Offset(-1,46)
		T28G C 1 Offset(-1,47)
		T28G A 1 Offset(0,49) A_WeaponReady(WRF_NOBOB)
		T28G A 1 Offset(0,44) A_WeaponReady(WRF_NOBOB)
		T28G A 1 Offset(0,38) A_WeaponReady(WRF_NOBOB)
		T28G A 1 Offset(0,35) A_WeaponReady(WRF_NOBOB)
		T28G A 1 Offset(0,32) A_WeaponReady(WRF_NOBOB)
		Goto Ready
	Flash:
		T28F A 1 BRIGHT A_Light2
		T28F B 1 BRIGHT A_Light1
		TNT1 A 1
		Goto LightDone
	Spawn:
		T28H A -1
		Stop
	}
}

ACTOR T1928Loaded : Ammo
{
	Tag ".45 ACP"
	+IGNORESKILL
	Inventory.MaxAmount 50
	Inventory.Icon FUDD01
}