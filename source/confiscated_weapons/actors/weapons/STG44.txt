// I'm very conflicted on where to put this. It fits both the automatics and the rifles slot at the same time. I think the only reason why I ended up putting it in the latter is 'cause of the assault RIFLE bit. Sucks to be the only one in the group before everyone else catches on the AR craze.

ACTOR STG44Casing : MauserRifleCasing{}

ACTOR 792KurzAmmo : Ammo
{
	Tag "7.92x33mm"
	Inventory.Amount 60
	Inventory.MaxAmount 150
	Ammo.BackpackAmount 90
	Ammo.BackpackMaxAmount 270
	Inventory.PickupMessage "Picked up a box of 7.92x33mm ammo."
	Inventory.Icon KURZ01
	States
	{
	Spawn:
		792K A -1
		Stop
	}
}

ACTOR MPStGTracer : PlayerTracer
{
	ProjectileKickback 100
	Damage (random(18,24))
	Speed 115
}

ACTOR STG44 : NaziWeapon
{
	//$Category Weapons (BoA Addons)
	//$Title (5) Sturmgewehr 44
	//$Color 14
	Weapon.SlotNumber 5
	Weapon.SelectionOrder 700
	inventory.pickupmessage "You got the Sturmgewehr 44!"
	Weapon.AmmoType "STG44Loaded"
	Weapon.AmmoUse 1
	weapon.ammotype2 "792KurzAmmo"
	weapon.ammouse2 1
	Weapon.AmmoGive1 30
	Weapon.AmmoGive2 60
	Scale .5
	Tag "Sturmgewehr 44"
	Weapon.UpSound "stg44/select"
	States
	{
	Ready:
		STGG A 0 A_JumpIfInventory("STG44Loaded",0,2)
		STGG A 0 A_JumpIfInventory("792KurzAmmo",1,2)
		STGG A 1 A_WeaponReady
		Loop
		STGG A 1 A_WeaponReady(WRF_ALLOWRELOAD)
		Loop
	Deselect:
		STGG A 0 A_Lower
		STGG A 1 A_Lower
		Loop
	Select:
		STGG A 0 A_Raise
		STGG A 1 A_Raise
		Loop
	Fire:
		STGG A 0 A_JumpIfInventory("STG44Loaded",1,1)
		Goto Dryfire
		STGG A 0 A_GunFlash
		TNT1 A 0 
		STGG B 0 A_PlaySound("stg44/fire",CHAN_WEAPON)
		STGG B 0 A_SpawnItemEx("STG44Casing",12,-20,32,8,random(-2,2),random(0,4),random(-55,-80),SXF_NOCHECKPOSITION)
		STGG B 0 A_AlertMonsters
		STGG B 1 A_FireCustomMissile("MPStGTracer",frandom(-1.0,1.0),1,0,0,0,frandom(-1.0,1.0))
		STGG B 1 Offset(0,38) A_SetPitch(pitch-(1.0*boa_recoilamount))
		STGG B 0 A_JumpIf(waterlevel > 0,2)
		STGG B 0 A_FireProjectile("ShotSmokeSpawner",0,0,0,random(-4,4),0,0)
		STGG B 1 Offset(0,37)
		STGG A 1 Offset(0,35)
		STGG A 1 Offset(0,33)
		TNT1 A 0 A_CheckReload
		Goto Ready
	Reload:
		STGG A 1 Offset(0,35) A_PlaySound("stg44/reload",5)
		STGG A 1 Offset(0,38) 
		STGG C 1 Offset(0,44)
		STGG C 1 Offset(-2,46)
		STGG C 1 Offset(-4,52)
		STGG C 1 Offset(-7,56)
		STGG C 1 Offset(-10,57)
		STGG C 8 Offset(-11,58)
	ReloadLoop:
		TNT1 A 0 A_TakeInventory("792KurzAmmo",1,TIF_NOTAKEINFINITE)
		TNT1 A 0 A_GiveInventory("STG44Loaded")
		TNT1 A 0 A_JumpIfInventory("STG44Loaded",0,"ReloadFinish")
		TNT1 A 0 A_JumpIfInventory("792KurzAmmo",1,"ReloadLoop")
	ReloadFinish:
		STGG C 1 Offset(-10,58)
		STGG C 1 Offset(-10,61)
		STGG C 1 Offset(-10,65)
		STGG C 1 Offset(-10,71)
		STGG D 1 Offset(-10,65)
		STGG D 1 Offset(-10,60)
		STGG D 1 Offset(-10,55)
		STGG D 1 Offset(-10,53)
		STGG D 1 Offset(-10,51)
		STGG D 1 Offset(-9,50)
		STGG D 1 Offset(-8,46)
		STGG D 1 Offset(-7,43)
		STGG A 1 Offset(-5,40)
		STGG A 1 Offset(-3,37)
		STGG A 1 Offset(-1,34)
		STGG A 1 Offset(0,32)
		Goto Ready
	Flash:
		STGF A 1 BRIGHT A_Light2
		STGF B 1 BRIGHT
		TNT1 A 2 A_Light1
		Goto LightDone
	Spawn:
		STGI A -1
		Stop
	}
}

ACTOR STG44Loaded : Ammo
{
	Tag "7.92x33mm"
	+IGNORESKILL
	Inventory.MaxAmount 30
	Inventory.Icon KURZ02
}