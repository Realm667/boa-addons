// Now you might ask yourself, why would Zik add grenades in Slot 9 instead of hijacking BoA's perfectly good grenade system? I couldn't get BoA's to show the different grenade types, nor have the ability to choose which grenades to throw. With the lack of coding knowledge to repair that particular problem, this is a decent enough workaround, I feel. 

// Yes, I considered replacing the OG Stielhandgranate to its own weapon as well to be consistent, but couldn't do it since it'd cause the HUD to not make sense.

ACTOR GeballteAmmo : Ammo
{
	Scale 0.25
	Tag "Components of Geballte Ladung Grenade"
	Inventory.Amount 1
	Inventory.MaxAmount 2
	Ammo.BackpackAmount 0
	Ammo.BackpackMaxAmount 4
	Inventory.PickupMessage "Someway, somehow, you managed to cobble up a whole new grenade out of spare parts while looking at the grenades you already have. Also, you're not actually supposed to see this, but I like to have fun with how people might handwave getting ammo for a weapon like this."
	Inventory.Icon GBLG01
	States
	{
	Spawn:
		GBLW A -1
		Stop
	}
}

ACTOR Geballte : NaziWeapon
{
	//$Category Weapons (Confiscated Weapons)
	//$Title (9) Geballte Ladung
	//$Color 14
	Weapon.SlotNumber 9
	Weapon.SelectionOrder 10000
	Inventory.PickupMessage "You got a Geballte Ladung!"
	Tag "Geballte Ladung"
	Weapon.AmmoUse 1
	Weapon.AmmoType "GeballteAmmo"
	Weapon.AmmoGive 2
	-WEAPON.AMMO_CHECKBOTH
	-WEAPON.NOALERT
	Scale 0.25
	Weapon.UpSound "grenade/select"
	Inventory.PickupSound "grenade/pickup"
	States
	{
	Select:
		GBLV A 0 A_Raise
		GBLV A 1 A_Raise
		Loop
	Deselect:
		TNT1 A 0 A_JumpIfInventory("GeballteAmmo",1,3)
		TNT1 A 0 A_Lower
		TNT1 A 1 A_Lower
		Loop
		GBLV A 0 A_Lower
		GBLV A 1 A_Lower
		Loop
	Ready:
		TNT1 A 0 A_JumpIfInventory("GeballteAmmo",1,2)
		TNT1 A 1 A_WeaponReady
		Loop
		GBLV A 1 A_WeaponReady
		Loop
	Fire:
		GBLV B 1 Offset(6,34)
		GBLV B 1 Offset(1,46)
		GBLV B 1 Offset(2,59)
		GBLV B 1 Offset(2,63)
		GBLV B 1 Offset(3,78)
		GBLV B 1 Offset(2,84)
		GBLV B 1 Offset(2,90) A_PlaySound("grenade/ringpull",CHAN_WEAPON)
		GBLV B 1 Offset(1,106) 
		GBLV B 1 Offset(1,114)
		GBLV B 1 Offset(6,122)
		GBLV B 1 Offset(3,130)
		TNT1 A 0 A_PlaySound("grenade/throw",CHAN_WEAPON)
		TNT1 A 2 Bright A_FireCustomMissile("Geballte_Thrown",0,1,0,0,0,0)
		TNT1 A 1 Offset(1,29)
		TNT1 A 1 Offset(-2,30)
		TNT1 A 1 Offset(-1,32)
		Goto Ready
	Spawn:
		GBLW A -1
		Loop
	}
}

Actor Geballte_Thrown : GrenadeBase
{
	Scale 0.25
	Mass 10
	Gravity 4
	Radius 4
	Height 3
	Speed 40
	Damage (random(4,12))
	DamageType "Frag"
	Reactiontime 8
	Projectile
	-NOGRAVITY
	-NOTELEPORT
	+WINDTHRUST
	BounceType "Doom"
	BounceFactor 0.4
	WallBounceFactor 0.6
	BounceSound "grenade/bounce"
	States
	{
	Spawn:
		GBLT A 2 A_CountDown
		GBLT BCDEFGH 2
		TNT1 A 0 A_JumpIf(waterlevel == 3, "AdjustMass")
		Loop
	Swim:
		GBLT A 2 A_CountDown() 
		TNT1 A 0 A_ScaleVelocity(0.7) 
		TNT1 A 0 A_SpawnItemEx("Bubble", 0, 0, 0, 0, 0, 2, random (0, 360), 0, 128)
		GBLT BCDEFGH 2
		Loop
	Death:
		GBLT A 35
		EXP1 A 0 A_SpawnGroundSplash
		EXP1 A 0 A_SetScale(0.75,0.75)
		EXP1 A 0 A_AlertMonsters
		EXP1 A 0 A_StopSound(CHAN_VOICE)
		EXP1 A 0 A_StartSound("panzer/explode",CHAN_AUTO, 0, 1.0, 0.1)
		EXP1 A 0 A_Explode(300)
		EXP1 A 0 A_SpawnItemEx("ZScorch")
		TNT1 AAAAAAAAAAAAAAAAAAAAAAAAAAA 0 A_SpawnItemEx("TracerSpark_Longlive", 0, 0, 0, random(-5,5), random(-5,5), random(-5,5), random(0,359)) 
		TNT1 A 8 A_SpawnItemEx("GeneralExplosion_Large",0,0,0)
		TNT1 A 2 BRIGHT LIGHT("BOAFLMW2") A_Quake(9,15,0,1024,"")
		TNT1 BCDEFGHIJKLMN 2 BRIGHT LIGHT("BOAFLMW2")
		Stop
	}
}