// I thought about doing an ammobelt bit with this gun. But then I'd have to jack up the frames for this gun, and then putting quite a bit more text on this code file. The gun isn't even that fully visible when I implemented it anyways, so I just left it like the others.

ACTOR 30CAL : NaziWeapon
{
	//$Category Weapons (Confiscated Weapons)
	//$Title (8) 30 CAL
	//$Color 14
	Scale 0.3
	Weapon.SlotNumber 8
	Weapon.SelectionOrder 750
	Weapon.AmmoType "30CALLoaded"
	Weapon.AmmoUse 1
	Weapon.AmmoType2 "3006Ammo"
	Weapon.AmmoUse2 1
	Weapon.AmmoGive1 50
	Weapon.AmmoGive2 100
	Tag "M1919 Browning MG"
	Inventory.PickupMessage "You got the M1919 .30 Cal!"
	Weapon.UpSound "m1919/select"
	States
	{
	Select:
		30CL A 0 A_Raise
		30CL A 1 A_Raise
		Loop
	Deselect:
		30CL A 0 A_Lower
		30CL A 1 A_Lower
		Loop
	Ready:
		30CL A 0 A_JumpIfInventory("30CALLoaded",0,2)
		30CL A 0 A_JumpIfInventory("3006Ammo",1,2)
		30CL A 1 A_WeaponReady
		Loop
		30CL A 1 A_WeaponReady(WRF_ALLOWRELOAD)
		Loop
	Fire:
		30CL A 0 A_JumpIfInventory("30CALLoaded",1,1)
		Goto Dryfire
		30CL A 0 A_GunFlash
		TNT1 A 0 
		30CL A 0 A_PlaySound("m1919/fire",CHAN_WEAPON)
		30CL A 0 A_SpawnItemEx("M1GarandRifleCasing",12,-20,32,8,random(-2,2),random(0,4),random(-55,-80),SXF_NOCHECKPOSITION)
		30CL A 0 A_AlertMonsters
		30CL B 1 A_FireCustomMissile("M1GarandTracer",frandom(-1.5,1.5),1,0,0,0,frandom(-1.0,1.0))
		30CL B 1 Offset(0,40) A_SetPitch(pitch-(1.6*boa_recoilamount))
		30CL A 0 A_JumpIf(waterlevel > 0,2)
		30CL A 0 A_FireProjectile("ChainSmokeSpawner",0,0,0,random(-4,4),0,0)
		30CL A 1 Offset(0,43) A_Refire
		30CL A 1 Offset(0,42) A_WeaponReady(WRF_NOBOB)
		30CL A 1 Offset(0,44) A_WeaponReady(WRF_NOBOB)
		30CL A 1 Offset(0,43) A_WeaponReady(WRF_NOBOB)
		30CL A 1 Offset(0,40) A_WeaponReady(WRF_NOBOB)
		30CL A 1 Offset(0,36) A_WeaponReady(WRF_NOBOB)
		30CL A 1 Offset(0,32) A_WeaponReady(WRF_NOBOB)
		Goto Ready
	Hold:
		30CL A 0 A_JumpIfInventory("30CALLoaded",1,1)
		Goto Dryfire
		30CL A 0 A_GunFlash
		TNT1 A 0 
		30CL A 0 A_PlaySound("m1919/fire",CHAN_WEAPON)
		30CL A 0 A_SpawnItemEx("M1GarandRifleCasing",12,-20,32,8,random(-2,2),random(0,4),random(-55,-80),SXF_NOCHECKPOSITION)
		30CL A 0 A_AlertMonsters
		30CL B 1 A_FireCustomMissile("M1GarandTracer",frandom(-1.5,1.5),1,0,0,0,frandom(-1.0,1.0))
		30CL B 1 Offset(0,52) A_SetPitch(pitch-(1.8*boa_recoilamount))
		30CL B 0 A_JumpIf(waterlevel > 0,2)
		30CL B 0 A_FireProjectile("ChainSmokeSpawner",0,0,0,random(-4,4),0,0)
		30CL A 1 Offset(0,53) A_Refire
		30CL A 1 Offset(0,52) A_WeaponReady(WRF_NOBOB)
		30CL A 1 Offset(0,54) A_WeaponReady(WRF_NOBOB)
		30CL A 1 Offset(0,53) A_WeaponReady(WRF_NOBOB)
		30CL A 1 Offset(0,50) A_WeaponReady(WRF_NOBOB)
		30CL A 1 Offset(0,46) A_WeaponReady(WRF_NOBOB)
		30CL A 1 Offset(0,41) A_WeaponReady(WRF_NOBOB)
		30CL A 1 Offset(0,35) A_WeaponReady(WRF_NOBOB)
		30CL A 1 Offset(0,32) A_WeaponReady(WRF_NOBOB)
		Goto Ready
	Reload:
		30CL A 1 Offset(0,35) A_PlaySound("m1919/reload",5)
		30CL A 1 Offset(0,38) 
		30CL A 1 Offset(0,44)
		30CL A 1 Offset(-2,46)
		30CL A 1 Offset(-4,52)
		30CL C 1 Offset(-7,56)
		30CL C 1 Offset(-10,57)
		30CL C 1 Offset(-11,58)
		30CL C 1 Offset(-2,46)
		30CL D 1 Offset(-4,52)
		30CL D 1 Offset(-7,56)
		30CL D 1 Offset(-10,57)
		30CL D 1 Offset(-10,65)
		30CL D 1 Offset(-11,58)
		30CL D 1 Offset(-2,46)
		30CL D 1 Offset(-4,52)
		30CL D 1 Offset(-7,56)
		30CL D 12 Offset(-11,58)
	ReloadLoop:
		30CL C 0 A_TakeInventory("3006Ammo",1,TIF_NOTAKEINFINITE)
		30CL C 0 A_GiveInventory("30CALLoaded")
		30CL C 0 A_JumpIfInventory("30CALLoaded",0,"ReloadFinish")
		30CL C 0 A_JumpIfInventory("3006Ammo",1,"ReloadLoop")
	ReloadFinish:
		30CL C 1 Offset(-10,57)
		30CL C 1 Offset(-11,58)
		30CL C 1 Offset(-2,46)
		30CL C 1 Offset(-4,52)
		30CL C 1 Offset(-7,56)
		30CL C 1 Offset(-10,58)
		30CL C 1 Offset(-10,61)
		30CL C 6 Offset(-10,65)
		30CL A 1 Offset(-10,71)
		30CL A 1 Offset(-10,65)
		30CL A 1 Offset(-10,60)
		30CL A 1 Offset(-10,55)
		30CL A 1 Offset(-10,53)
		30CL A 1 Offset(-10,51)
		30CL A 1 Offset(-9,50)
		30CL E 6 Offset(-8,46)
		30CL E 6 Offset(-7,43)
		30CL E 1 Offset(-5,40)
		30CL E 1 Offset(-3,37)
		30CL A 1 Offset(-1,34)
		30CL A 1 Offset(0,32)
		Goto Ready
	Flash:
		30CF A 1 BRIGHT A_Light2
		30CF B 1 BRIGHT
		TNT1 A 2 A_Light1
		Goto LightDone
	Spawn:
		30CW A -1
		Stop
	}
}

ACTOR 30CALLoaded : Ammo
{
	Tag ".30-06"
	+IGNORESKILL
	Inventory.MaxAmount 50
	Inventory.Icon GARA03
}