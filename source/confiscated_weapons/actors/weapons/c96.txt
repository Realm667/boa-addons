// This pistol wasn't even supposed to be the Schnellfeuer (if the code hasn't told you enough), just a regular ol' C96. Well, until I realized that it's too much like the Walther P38 in performance. Yes, I know the Schnellfeuer has both detachable magazine and a stock, but I wanted a reload that's different from the other pistols, so I'm keeping the old C96 reloading frames. Getting a modelshot with a stock is possible, but the accuracy increase would make things like the 9mm Slot 4 weapons a bit less useful. At the end, I pretty much just upped the firerate and recoil with a name swap. An emergency bullethose type of pistol.

ACTOR C96Tracer : WaltherTracer
{
	Speed 100
}

ACTOR C96 : NaziWeapon
{
	//$Category Weapons (Confiscated Weapons)
	//$Title (2) M712
	//$Color 14
	Scale 0.5
	Weapon.SlotNumber 2
	Weapon.SelectionOrder 9997
	Weapon.AmmoType "MauserC96Loaded"
	Weapon.AmmoUse 1
	weapon.ammotype2 "Ammo9mm"
	weapon.ammouse2 1
	Tag "M712 Schnellfeuer"
	Inventory.PickupMessage "You got the Mauser M712 Schnellfeuer!"
	Weapon.UpSound "c96/select"
	Weapon.AmmoGive1 10
	Weapon.AmmoGive2 20
	States
	{
	Ready:
		MC96 A 0 A_JumpIfInventory("MauserC96Loaded",0,2)
		MC96 A 0 A_JumpIfInventory("Ammo9mm",1,2)
		MC96 A 1 A_WeaponReady
		Loop
		MC96 A 1 A_WeaponReady(WRF_ALLOWRELOAD)
		Loop
	Deselect:
		MC96 A 0 A_Lower
		MC96 A 1 A_Lower
		Loop
	Select:
		MC96 A 0 A_Raise
		MC96 A 1 A_Raise
		Loop
	Fire:
		MC96 A 0 A_JumpIfInventory("MauserC96Loaded",1,1)
		Goto Dryfire
		MC96 A 0 A_GunFlash
		MC96 A 0 A_SetPitch(pitch-(0.2*boa_recoilamount))
		MC96 A 0 A_JumpIf(waterlevel > 0,2)
		MC96 A 0 A_FireProjectile("PistolSmokeSpawner",0,0,0,random(-4,4),0,0)
		MC96 A 0 A_PlaySound("c96/fire",CHAN_WEAPON)
		MC96 A 0 A_SpawnItemEx("Casing9mm",12,-20,32,8,random(-2,2),random(0,4),random(-55,-80),SXF_NOCHECKPOSITION)
		MC96 A 0 A_AlertMonsters
		MC96 A 1 A_FireCustomMissile("C96Tracer",frandom(-1.5,1.5))
		MC96 B 1 A_SetPitch(pitch-(1.4*boa_recoilamount))
		MC96 B 1 Offset(0,36) A_Refire
		MC96 A 1 Offset(0,35)
		TNT1 A 0 A_CheckReload
		MC96 A 1 Offset(0,32) A_Jump(128,"Ready")
		Goto Ready
	Reload:
		MC96 C 1 Offset(0,35) 
		MC96 C 1 Offset(0,38)
		MC96 C 1 Offset(0,44)
		MC96 D 1 Offset(0,52) A_StartSound("c96/reload", CHAN_5)
		MC96 D 1 Offset(0,62)
		MC96 E 1 Offset(0,72)
		MC96 E 1 Offset(0,82)
		TNT1 A 8
	ReloadLoop:
		TNT1 A 0 A_TakeInventory("Ammo9mm",1,TIF_NOTAKEINFINITE)
		TNT1 A 0 A_GiveInventory("MauserC96Loaded")
		TNT1 A 0 A_JumpIfInventory("MauserC96Loaded",0,"ReloadFinish")
		TNT1 A 0 A_JumpIfInventory("Ammo9mm",1,"ReloadLoop")
	ReloadFinish:
		MC96 C 1 Offset(0,82)
		MC96 C 1 Offset(0,72)
		MC96 C 1 Offset(0,62)
		MC96 C 1 Offset(0,52)
		MC96 A 1 Offset(0,44)
		MC96 A 1 Offset(0,38)
		MC96 A 1 Offset(0,35)
		MC96 A 1 Offset(0,32)
		Goto Ready
	Flash:
		MC96 F 1 BRIGHT A_Light2
		TNT1 A 1
		TNT1 A 2 A_Light1
		Goto LightDone
	Spawn:
		MC96 W -1
		Stop
	}
}

ACTOR MauserC96Loaded : Ammo
{
	Tag "9x19mm"
	+IGNORESKILL
	Inventory.MaxAmount 10
	Inventory.Icon WALT03
}