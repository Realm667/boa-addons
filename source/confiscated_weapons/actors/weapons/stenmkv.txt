// Another darling that was also in Hidden & Dangerous that I just couldn't miss to implement after hitting the 40-to-50 range. It does have some differences performance-wise to the regular Sten MK2S, especially now that you got a proper place to put your hand on instead of just somewhere on the pipe. Tried my best not to have the differences be TOO significant, though. 

ACTOR STNV : NaziWeapon
{
	//$Category Weapons (BoA)
	//$Title (4) Sten MKV
	//$Color 14
	Scale 0.30
	Weapon.SlotNumber 4
	Weapon.SelectionOrder 600
	Inventory.PickupMessage "You got the Sten MKV!"
	Weapon.AmmoType "STENMKVLoaded"
	Weapon.AmmoUse 1
	Weapon.AmmoType2 "Ammo9mm"
	Weapon.AmmoUse2 1
	Weapon.AmmoGive1 32
	Weapon.AmmoGive2 64
	Weapon.UpSound "sten/select"
	Tag "Sten MKV"
	States
	{
	Ready:
		STNV A 0 A_JumpIfInventory("STENMKVLoaded",0,2)
		STNV A 0 A_JumpIfInventory("Ammo9mm",1,2)
		STNV A 1 A_WeaponReady
		Loop
		STNV A 1 A_WeaponReady(WRF_ALLOWRELOAD)
		Loop
	Deselect:
		STNV A 0 A_Lower
		STNV A 1 A_Lower
		Loop
	Select:
		STNV A 0 A_Raise
		STNV A 1 A_Raise
		Loop
	Fire:
		STNV A 0 A_JumpIfInventory("STENMKVLoaded",1,1)
		Goto Dryfire
		STNV A 0 A_GunFlash
		STNV A 0 A_StartSound("stennosup/fire", CHAN_WEAPON)
		STNV A 0 A_SpawnItemEx("Casing9mm",12,-20,32,8,random(-2,2),random(0,4),random(-55,-80),SXF_NOCHECKPOSITION)
		STNV A 0 A_AlertMonsters
		STNV B 1 A_FireProjectile("MP40Tracer",frandom(-1.0,1.0),1,0,0,0,frandom(-1.0,1.0))
		STNV B 1 Offset(0,40) A_SetPitch(pitch-(0.5*boa_recoilamount))
		STNV B 0 A_JumpIf(waterlevel > 0,2)
		STNV B 0 A_FireProjectile("ChainSmokeSpawner",0,0,0,random(-4,4),0,0)
		STNV A 1 Offset(0,34)
		TNT1 A 0 A_Refire
		Goto Ready
	Hold:
		STNV A 0 A_JumpIfInventory("STENMKVLoaded",1,1)
		Goto Dryfire
		STNV A 0 A_GunFlash
		STNV A 0 A_StartSound("stennosup/fire", CHAN_WEAPON)
		STNV A 0 A_SpawnItemEx("Casing9mm",12,-20,32,8,random(-2,2),random(0,4),random(-55,-80),SXF_NOCHECKPOSITION)
		STNV A 0 A_AlertMonsters
		STNV B 1 Offset(0,36) A_FireProjectile("MP40Tracer",frandom(-1.0,1.0),1,0,0,0,frandom(-1.0,1.0))
		STNV B 0 A_JumpIf(waterlevel > 0,2)
		STNV B 0 A_FireProjectile("ChainSmokeSpawner",0,0,0,random(-4,4),0,0)
		STNV B 1 Offset(0,40) A_SetPitch(pitch-(0.5*boa_recoilamount))
		STNV A 1 Offset(0,34)
		TNT1 A 0 A_Refire
		Goto Ready
	Reload:
		STNV A 1 Offset(0,38) A_StartSound("sten/reload", CHAN_5)
		STNV A 1 Offset(0,44)
		STNV A 1 Offset(0,56)
		STNV A 1 Offset(-2,60)
		STNV A 1 Offset(-4,72)
		STNV A 1 Offset(-7,80)
		STNV C 1 Offset(-10,82)
		STNV C 20 Offset(-11,84)
	ReloadLoop:
		TNT1 A 0 A_TakeInventory("Ammo9mm",1,TIF_NOTAKEINFINITE)
		TNT1 A 0 A_GiveInventory("STENMKVLoaded")
		TNT1 A 0 A_JumpIfInventory("STENMKVLoaded",0,"ReloadFinish")
		TNT1 A 0 A_JumpIfInventory("Ammo9mm",1,"ReloadLoop")
	ReloadFinish:
		STNV D 1 Offset(-10,84)
		STNV D 1 Offset(-10,90)
		STNV D 1 Offset(-10,98)
		STNV D 1 Offset(-10,110)
		STNV A 1 Offset(-10,98)
		STNV A 1 Offset(-10,88)
		STNV A 1 Offset(-10,78)
		STNV A 1 Offset(-10,74)
		STNV A 1 Offset(-10,70)
		STNV A 1 Offset(-9,68)
		STNV A 1 Offset(-8,60)
		STNV A 1 Offset(-7,54)
		STNV A 1 Offset(-5,48)
		STNV A 1 Offset(-3,42)
		STNV A 1 Offset(-1,36)
		STNV A 1 Offset(0,32)
		Goto Ready
	Flash:
		STVF A 1 BRIGHT A_Light2
		STVF B 1 BRIGHT
		TNT1 A 2 A_Light1
		Goto LightDone
	Spawn:
		STNV Z -1
		Stop
	}
}

ACTOR STENMKVLoaded : Ammo
{
	Tag "9x19mm"
	Inventory.MaxAmount 32
	+IGNORESKILL
	Inventory.Icon WALT01
}