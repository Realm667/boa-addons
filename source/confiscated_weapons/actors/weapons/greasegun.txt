// Remember what I said regarding weapons that look exactly the same core wise but with slight differences in the AVS36? I did consider a suppressed Grease Gun, but tossed it because of that thought process.

ACTOR GreaseTracer : M1ThompsonTracer
{
	Speed 100
}

ACTOR M3Grease : NaziWeapon
{
	//$Category Weapons (Confiscated Weapons)
	//$Title (4) M3 Grease Gun
	//$Color 14
	Weapon.SlotNumber 4
	Weapon.SelectionOrder 700
	inventory.pickupmessage "You got the M3 Grease Gun!"
	Weapon.AmmoType "M3GreaseLoaded"
	Weapon.AmmoUse 1
	weapon.ammotype2 "45ACPAmmo"
	weapon.ammouse2 1
	Weapon.AmmoGive1 30
	Weapon.AmmoGive2 60
	Scale .4
	Tag "M3 Grease Gun"
	Weapon.UpSound "greasegun/select"
	States
	{
	Ready:
		M3GG A 0 A_JumpIfInventory("M3GreaseLoaded",0,2)
		M3GG A 0 A_JumpIfInventory("45ACPAmmo",1,2)
		M3GG A 1 A_WeaponReady
		Loop
		M3GG A 1 A_WeaponReady(WRF_ALLOWRELOAD)
		Loop
	Deselect:
		M3GG A 0 A_Lower
		M3GG A 1 A_Lower
		Loop
	Select:
		M3GG A 0 A_Raise
		M3GG A 1 A_Raise
		Loop
	Fire:
		M3GG A 0 A_JumpIfInventory("M3GreaseLoaded",1,1)
		Goto Dryfire
		M3GG A 0 A_GunFlash
		TNT1 A 0 
		M3GG B 0 A_PlaySound("greasegun/fire",CHAN_WEAPON)
		M3GG B 0 A_SpawnItemEx("45ACPCasing",12,-20,32,8,random(-2,2),random(0,4),random(-55,-80),SXF_NOCHECKPOSITION)
		M3GG B 0 A_AlertMonsters
		M3GG B 1 A_FireCustomMissile("GreaseTracer",frandom(-1.0,1.0),1,0,0,0,frandom(-1.0,1.0))
		M3GG B 1 Offset(0,38) A_SetPitch(pitch-(0.5*boa_recoilamount))
		M3GG B 0 A_JumpIf(waterlevel > 0,2)
		M3GG B 0 A_FireProjectile("ChainSmokeSpawner",0,0,0,random(-4,4),0,0)
		M3GG B 1 Offset(0,37)
		M3GG A 1 Offset(0,35)
		M3GG A 1 Offset(0,33)
		TNT1 A 0 A_CheckReload
		Goto Ready
	Reload:
		M3GG A 1 Offset(0,35) A_PlaySound("greasegun/reload",5)
		M3GG A 1 Offset(0,38) 
		M3GG C 1 Offset(0,44)
		M3GG C 1 Offset(-2,46)
		M3GG C 1 Offset(-4,52)
		M3GG C 1 Offset(-7,56)
		M3GG C 1 Offset(-10,57)
		M3GG C 8 Offset(-11,58)
	ReloadLoop:
		TNT1 A 0 A_TakeInventory("45ACPAmmo",1,TIF_NOTAKEINFINITE)
		TNT1 A 0 A_GiveInventory("M3GreaseLoaded")
		TNT1 A 0 A_JumpIfInventory("M3GreaseLoaded",0,"ReloadFinish")
		TNT1 A 0 A_JumpIfInventory("45ACPAmmo",1,"ReloadLoop")
	ReloadFinish:
		M3GG C 1 Offset(-10,58)
		M3GG C 1 Offset(-10,61)
		M3GG C 1 Offset(-10,65)
		M3GG C 1 Offset(-10,71)
		M3GG D 1 Offset(-10,65)
		M3GG D 1 Offset(-10,60)
		M3GG D 1 Offset(-10,55)
		M3GG D 1 Offset(-10,53)
		M3GG D 1 Offset(-10,51)
		M3GG D 1 Offset(-9,50)
		M3GG D 1 Offset(-8,46)
		M3GG D 1 Offset(-7,43)
		M3GG A 1 Offset(-5,40)
		M3GG A 1 Offset(-3,37)
		M3GG A 1 Offset(-1,34)
		M3GG A 1 Offset(0,32)
		Goto Ready
	Flash:
		M3GF A 1 BRIGHT A_Light2
		M3GF B 1 BRIGHT
		TNT1 A 2 A_Light1
		Goto LightDone
	Spawn:
		M3GW A -1
		Stop
	}
}

ACTOR M3GreaseLoaded : Ammo
{
	Tag ".45 ACP"
	+IGNORESKILL
	Inventory.MaxAmount 30
	Inventory.Icon FUDD01
}