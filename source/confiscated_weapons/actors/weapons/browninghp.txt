// At this point, I realized that pretty much all of the pistols in Confiscated Weapons have no autofire. Some of them for sensible reasons like being an ammo saving option to a rapid fire alternate, and then there's this where if I removed the autofire, it would've made the Walther P38 completely useless, being a straight up upgrade. Yeah, it's that warped sense of balance again.

ACTOR BrownHP : NaziWeapon
{
	//$Category Weapons (Confiscated Weapons)
	//$Title (2) Browning Hi-Power
	//$Color 14
	Scale 0.5
	Weapon.SlotNumber 2
	Weapon.SelectionOrder 9997
	Weapon.AmmoType "BrowningHPLoaded"
	Weapon.AmmoUse 1
	weapon.ammotype2 "Ammo9mm"
	weapon.ammouse2 1
	Tag "Browning Hi-Power"
	Inventory.PickupMessage "You got the Browning Hi-Power!"
	Weapon.UpSound "browhp/select"
	Weapon.AmmoGive1 13
	Weapon.AmmoGive2 26
	+WEAPON.NOAUTOFIRE
	States
	{
	Ready:
		BRHP A 0 A_JumpIfInventory("BrowningHPLoaded",0,2)
		BRHP A 0 A_JumpIfInventory("Ammo9mm",1,2)
		BRHP A 1 A_WeaponReady
		Loop
		BRHP A 1 A_WeaponReady(WRF_ALLOWRELOAD)
		Loop
	Deselect:
		BRHP A 0 A_Lower
		BRHP A 1 A_Lower
		Loop
	Select:
		BRHP A 0 A_Raise
		BRHP A 1 A_Raise
		Loop
	Fire:
		BRHP A 0 A_JumpIfInventory("BrowningHPLoaded",1,1)
		Goto Dryfire
		BRHP A 0 A_GunFlash
		BRHP A 0 A_SetPitch(pitch-(0.2*boa_recoilamount))
		BRHP A 0 A_JumpIf(waterlevel > 0,2)
		BRHP A 0 A_FireProjectile("PistolSmokeSpawner",0,0,0,random(-4,4),0,0)
		BRHP A 0 A_PlaySound("browhp/fire",CHAN_WEAPON)
		BRHP A 0 A_SpawnItemEx("Casing9mm",12,-20,32,8,random(-2,2),random(0,4),random(-55,-80),SXF_NOCHECKPOSITION)
		BRHP A 0 A_AlertMonsters
		BRHP A 1 A_FireCustomMissile("WaltherTracer")
		BRHP B 2
		TNT1 A 0 
		BRHP A 1 A_SetPitch(pitch-(0.2*boa_recoilamount))
		BRHP A 1 Offset(0,36)
		BRHP A 1 Offset(0,41)
		BRHP A 1 Offset(0,35)
		TNT1 A 0 A_CheckReload
		BRHP A 1 Offset(0,32) A_Jump(128,"Ready")
		Goto Ready
	Reload:
		BRHP C 1 Offset(0,35) A_StartSound("browhp/reload", CHAN_5)
		BRHP C 1 Offset(0,38)
		BRHP C 1 Offset(0,44)
		BRHP C 1 Offset(0,52)
		BRHP C 1 Offset(0,62)
		BRHP C 1 Offset(0,72)
		BRHP C 1 Offset(0,82)
		TNT1 A 8
	ReloadLoop:
		TNT1 A 0 A_TakeInventory("Ammo9mm",1,TIF_NOTAKEINFINITE)
		TNT1 A 0 A_GiveInventory("BrowningHPLoaded")
		TNT1 A 0 A_JumpIfInventory("BrowningHPLoaded",0,"ReloadFinish")
		TNT1 A 0 A_JumpIfInventory("Ammo9mm",1,"ReloadLoop")
	ReloadFinish:
		BRHP C 1 Offset(0,82)
		BRHP C 1 Offset(0,72)
		BRHP C 1 Offset(0,62)
		BRHP C 1 Offset(0,52)
		BRHP A 1 Offset(0,44)
		BRHP A 1 Offset(0,38)
		BRHP A 1 Offset(0,35)
		BRHP A 1 Offset(0,32)
		Goto Ready
	Flash:
		BRHP F 1 BRIGHT A_Light2
		TNT1 A 1
		TNT1 A 2 A_Light1
		Goto LightDone
	Spawn:
		BRHP W -1
		Stop
	}
}

ACTOR BrowningHPLoaded : Ammo
{
	Tag "9x19mm"
	+IGNORESKILL
	Inventory.MaxAmount 13
	Inventory.Icon WALT01
}