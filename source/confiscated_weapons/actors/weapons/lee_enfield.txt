// A thought crossed my mind around this point if I should implement a Brothers in Arms RTH30-style reload for rifles with stripper clip kind of loads. Full stripper if empty, single loads if less than stripper amount. Then I thought, wouldn't stripper clips still work anyways, it'll just be blocked when the internal mag's full? I decided then to call off the silly notion, probably for the best.

ACTOR Enfield : NaziWeapon
{
	//$Category Weapons (Confiscated Weapons)
	//$Title (5) Lee Enfield
	//$Color 14
	Scale 0.45
	Weapon.SlotNumber 5
	Weapon.SelectionOrder 750
	Weapon.AmmoType "EnfieldLoaded"
	Weapon.AmmoUse 1
	Weapon.AmmoType2 "303Ammo"
	Weapon.AmmoUse2 1
	Tag "Lee-Enfield SMLE"
	Inventory.PickupMessage "You got the Lee-Enfield SMLE!"
	+WEAPON.NOAUTOFIRE
	Weapon.UpSound "leeenfield/select"
	Weapon.AmmoGive1 10
	Weapon.AmmoGive2 20
	States
	{
	Select:
		ENFG A 0 A_Raise
		ENFG A 1 A_Raise
		Loop
	Deselect:
		ENFG A 0 A_Lower
		ENFG A 1 A_Lower
		Loop
	Ready:
		ENFG A 0 A_JumpIfInventory("EnfieldLoaded",0,2)
		ENFG A 0 A_JumpIfInventory("303Ammo",1,2)
		ENFG A 1 A_WeaponReady
		Loop
		ENFG A 1 A_WeaponReady(WRF_ALLOWRELOAD)
		Loop
	Fire:
		ENFG A 0 A_JumpIfInventory("EnfieldLoaded",1,1)
		Goto Dryfire
		ENFG A 0 A_GunFlash
		ENFG A 0 A_PlaySound("leeenfield/fire",CHAN_WEAPON)
		ENFG A 0 A_AlertMonsters
		ENFG A 1 A_FireCustomMissile("BrenfieldTracer")
		ENFG B 1 Offset(0,52)
		ENFG B 1 Offset(0,45) A_SetPitch(pitch-(4.0*boa_recoilamount))
		ENFG B 1 Offset(0,40) A_SetPitch(pitch-(2.0*boa_recoilamount))
		ENFG B 1 Offset(0,36) A_SetPitch(pitch+(1.0*boa_recoilamount))
		ENFG B 1 Offset(0,34) A_SetPitch(pitch+(1.0*boa_recoilamount))
		ENFG B 1 Offset(0,33) A_SetPitch(pitch+(0.5*boa_recoilamount))
		ENFG C 1 Offset(-2,33) A_PlaySound("leeenfield/open",5)
		ENFG C 1 Offset(-4,34)
		ENFG D 1 Offset(-6,35)
		ENFG E 2 Offset(-8,36)
		ENFG F 2 Offset(-4,42)
		ENFG G 1 Offset(0,51) A_SpawnItemEx("EnfieldRifleCasing",12,-20,32,8,random(-2,2),random(0,4),random(-55,-80),SXF_NOCHECKPOSITION)
		ENFG G 1 Offset(4,60)
		ENFG G 2 Offset(5,74)
		ENFG G 3 Offset(6,76)
		ENFG F 2 Offset(5,74) A_PlaySound("leeenfield/shut",5)
		ENFG F 1 Offset(4,60)
		ENFG E 1 Offset(0,51)
		ENFG E 1 Offset(-4,42)
		ENFG D 2 Offset(-8,36)
		ENFG C 1 Offset(-6,35)
		ENFG A 1 Offset(-4,34)
		ENFG A 1 Offset(-2,33)
		TNT1 A 0 A_CheckReload
		Goto Ready
	Reload:
		ENFG A 0
		ENFG C 1 Offset(-2,33) 
		ENFG C 1 Offset(-4,34) 
		ENFG C 1 Offset(-6,35)
		ENFG D 2 Offset(-8,36) A_PlaySound("leeenfield/open",5)
		ENFG D 2 Offset(-4,42)
		ENFG E 1 Offset(0,51)
		ENFG E 1 Offset(4,60)
		ENFG F 2 Offset(5,74)
		ENFG F 3 Offset(6,76)
		ENFG G 2 Offset(5,74)
		ENFG G 8 Offset(6,76)
		ENFG A 0 A_JumpIfInventory("EnfieldLoaded",5,"ReloadLoop2")
		ENFG A 0 A_JumpIfInventory("303Ammo",5,1)
		Goto ReloadLoop2
		TNT1 A 0 A_TakeInventory("303Ammo",5)
		TNT1 A 0 A_GiveInventory("EnfieldLoaded",5)
		ENFG H 1 Offset(6,80) A_PlaySound("leeenfield/insert",5)
		ENFG H 1 Offset(6,84)
		ENFG G 1 Offset(6,87)
		ENFG G 1 Offset(7,90)
		ENFG G 1 Offset(8,92)
		ENFG G 1 Offset(9,88)
		ENFG G 1 Offset(8,82)
		ENFG G 1 Offset(7,77)
		ENFG G 0 A_JumpIfInventory("EnfieldLoaded",0,"ReloadOnlyOneClip")
		TNT1 A 0 A_JumpIfInventory("303Ammo",1,1)
		Goto ReloadOnlyOneClip
		ENFG A 5 Offset(6,75)
	ReloadLoop2:
		TNT1 A 0 A_TakeInventory("303Ammo",1)
		TNT1 A 0 A_GiveInventory("EnfieldLoaded")
		TNT1 A 0 A_JumpIfInventory("EnfieldLoaded",0,"ReloadFinish")
		TNT1 A 0 A_JumpIfInventory("303Ammo",1,"ReloadLoop2")
	ReloadFinish:
		ENFG H 1 Offset(6,80) A_PlaySound("leeenfield/insert",5)
		ENFG H 1 Offset(6,84)
		ENFG G 1 Offset(6,87)
		ENFG G 1 Offset(7,90)
		ENFG G 1 Offset(8,92)
		ENFG G 1 Offset(9,88)
		ENFG G 1 Offset(8,82)
		ENFG G 1 Offset(7,77)
	ReloadOnlyOneClip:
		ENFG G 2 Offset(5,74) A_PlaySound("leeenfield/shut",5)
		ENFG G 1 Offset(4,60)
		ENFG F 1 Offset(0,51)
		ENFG F 1 Offset(-4,42)
		ENFG E 2 Offset(-8,36)
		ENFG D 1 Offset(-6,35)
		ENFG C 1 Offset(-4,34)
		ENFG A 1 Offset(-2,33)
		Goto Ready
	Flash:
		ENFF A 1 BRIGHT A_Light2
		ENFF B 1 BRIGHT
		TNT1 A 2 A_Light1
		Goto LightDone
	Spawn:
		ENFP A -1
		Stop
	}
}

ACTOR EnfieldLoaded : Ammo
{
	Tag ".303 British"
	+IGNORESKILL
	Inventory.MaxAmount 10
	Inventory.Icon 303C01
}