// Another in the "How Do I Make Another Knife Implement That's Not Just the Regular Knife But Looking Different" entry of the Slot 1 additions. A silent takedown option that's actually deadly in one hit this time against low tier enemies, but with a twist. A chance for the players to accidentally hit the wall and have the knife bounce straight back to their skulls in an amusingly hilarious display of shenanigans.

ACTOR KnifeAmmo : Ammo
{
	Scale 0.2
	Tag "Throwing Knife"
	Inventory.Amount 1
	Inventory.MaxAmount 10
	Ammo.BackpackAmount 0
	Ammo.BackpackMaxAmount 20
	Inventory.PickupMessage "Picked up a previously thrown knife."
	Inventory.Icon KNIF01
	States
	{
	Spawn:
		TKNT A -1
		Stop
	}
}

ACTOR ThrowingKnife : NaziWeapon
{
	Weapon.SlotNumber 1
	Weapon.SelectionOrder 10000
	Inventory.PickupMessage "You got a Throwing Knife! Careful not to hit yourself with it."
	Tag "Throwing Knife"
	Weapon.AmmoUse 1
	Weapon.AmmoType "KnifeAmmo"
	Weapon.AmmoGive 2
	-WEAPON.AMMO_CHECKBOTH
	+WEAPON.NOALERT
	Scale .2
	Weapon.UpSound "select/knife"
	Inventory.PickupSound "grenade/pickup"
	States
	{
	Select:
		TKNV A 0 A_Raise
		TKNV A 1 A_Raise
		Loop
	Deselect:
		TNT1 A 0 A_JumpIfInventory("KnifeAmmo",1,3)
		TNT1 A 0 A_Lower
		TNT1 A 1 A_Lower
		Loop
		TKNV A 0 A_Lower
		TKNV A 1 A_Lower
		Loop
	Ready:
		TNT1 A 0 A_JumpIfInventory("KnifeAmmo",1,2)
		TNT1 A 1 A_WeaponReady
		Loop
		TKNV A 1 A_WeaponReady
		Loop
	Fire:
		TKNV A 1 Offset(6,34)
		TKNV A 1 Offset(1,46)
		TKNV A 1 Offset(2,59)
		TKNV A 1 Offset(2,63)
		TKNV A 1 Offset(3,78)
		TKNV A 1 Offset(2,84)
		TKNV B 0 A_PlaySound("weapons/thrownoclick",CHAN_WEAPON)
		TKNV B 2 Bright A_FireCustomMissile("Knife_Thrown",0,1,0,0,0,0)
		TKNV B 1 Offset(2,90)
		TKNV B 1 Offset(1,106) 
		TKNV B 1 Offset(1,114)
		TKNV B 1 Offset(6,122)
		TKNV B 1 Offset(3,130)
		TNT1 A 1 Offset(1,29)
		TNT1 A 1 Offset(-2,30)
		TNT1 A 1 Offset(-1,32)
		Goto Ready
	Spawn:
		TKNT A -1
		Loop
	}
}

Actor Knife_Thrown
{
	Scale .2
	Radius 4
	Height 3
	Speed 40
	Damage 20
	Reactiontime 8 //for countdown
	Projectile
	-NOGRAVITY
	-NOTELEPORT
	+WINDTHRUST
	+NOEXTREMEDEATH
	BounceType "Doom"
	BounceFactor 0.4
	WallBounceFactor 0.6
	BounceSound "knife/stone"
	States
	{
	Spawn:
		TKNT A 2
		TKNT BCDEFGH 2 A_ChangeFlag("HITOWNER", true)
		TNT1 A 0 A_JumpIf(waterlevel == 3, "AdjustMass")
		Loop
	Swim:
		TKNT A 2 A_CountDown() 
		TNT1 A 0 A_ScaleVelocity(0.7) 
		TNT1 A 0 A_SpawnItemEx("Bubble", 0, 0, 0, 0, 0, 2, random (0, 360), 0, 128)
		TKNT BCDEFGH 2
		Loop
	Death:
		TNT1 A 0 A_AlertMonsters(192)
		TNT1 A 0 A_SpawnItemEx("KnifePuff")
		TNT1 A 0 A_SpawnItemEx("KnifeAmmo")
		Stop
	}
}