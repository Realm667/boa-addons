// I wish I could have the satchel charge be limited in range use, felt a bit cheaty that you could deploy this in one end of the map and then detonate it at the whole opposite end. Couldn't figure out how to do wires ala' HDest, so this thing'll stay for the moment. I didn't even mean to make it a detonator-type weapon at first, I wanted a mine-type weapon. Wasn't until I looked at the shop in one of my test runs when I found out that there's already a mine in base BoA, and so the lever detonated satchel charge was the final destination.

ACTOR SatchelAmmo : Ammo
{
	Scale 0.3
	Tag "Components of Satchel Charge"
	Inventory.Amount 1
	Inventory.MaxAmount 2
	Ammo.BackpackAmount 0
	Ammo.BackpackMaxAmount 4
	Inventory.PickupMessage "Someway, somehow, you managed to cobble up a whole new satchel charge out of spare parts while looking at the ones you already have. Also, you're not actually supposed to see this, but I like to have fun with how people might handwave getting ammo for a weapon like this."
	Inventory.Icon SATT01
	States
	{
	Spawn:
		SATT A -1
		Stop
	}
}

ACTOR PlayerSatchels : NaziWeapon
{
	//$Category Weapons (Confiscated Weapons)
	//$Title (9) Satchel Charge
	//$Color 14
	Weapon.SlotNumber 9
	Weapon.SelectionOrder 10000
	Inventory.PickupMessage "You got a Satchel Charge! Hitting alternate fire'll allow you to detonate all thrown charges."
	Tag "Satchel Charge"
	Weapon.AmmoUse 0
	Weapon.AmmoType "SatchelAmmo"
	Weapon.AmmoGive 2
	-WEAPON.AMMO_CHECKBOTH
	-WEAPON.NOALERT
	Scale 0.3
	Weapon.UpSound "grenade/select"
	Inventory.PickupSound "grenade/pickup"
	States
	{
	Select:
		TNT1 A 0 A_JumpIfInventory("SatchelAmmo",1,2)
		TNT1 A 1 A_WeaponReady
		Loop
		SATV A 0 A_Raise
		SATV A 1 A_Raise
		Loop
	Deselect:
		TNT1 A 0 A_JumpIfInventory("SatchelAmmo",1,3)
		TNT1 A 0 A_Lower
		TNT1 A 1 A_Lower
		Loop
		SATV A 0 A_Lower
		SATV A 1 A_Lower
		Loop
	Ready:
		TNT1 A 0 A_JumpIfInventory("SatchelAmmo",1,2)
		TNT1 A 1 A_WeaponReady
		Loop
		SATV A 1 A_WeaponReady
		Loop
	Fire:
		SATV A 1 Offset(6,34)
		SATV A 1 Offset(1,46)
		SATV A 1 Offset(2,59)
		SATV A 1 Offset(2,63)
		SATV A 1 Offset(3,78)
		SATV A 1 Offset(2,84)
		SATV A 1 Offset(2,90) A_PlaySound("weapon/dryfire",CHAN_WEAPON)
		SATV A 1 Offset(1,106) 
		SATV A 1 Offset(1,114)
		SATV A 1 Offset(6,122)
		SATV A 1 Offset(3,130)
		TNT1 A 0 A_PlaySound("weapons/thrownoclick",CHAN_WEAPON)
		TNT1 A 2 Bright A_FireCustomMissile("Satchel_Thrown")
		TNT1 A 0 A_TakeInventory("SatchelAmmo",1,TIF_NOTAKEINFINITE)
		TNT1 A 1 Offset(1,29)
		TNT1 A 1 Offset(-2,30)
		TNT1 A 1 Offset(-1,32)
		Goto Ready
	 AltFire:
		"####" A 1 Offset(6,34)
		"####" A 1 Offset(1,46)
		"####" A 1 Offset(2,59)
		"####" A 1 Offset(2,63)
		"####" A 1 Offset(3,78)
		"####" A 1 Offset(2,84)
		DETV A 1 Offset(3,78)
		DETV A 1 Offset(2,59)
		DETV A 1 Offset(1,46)
		DETV A 1 Offset(6,34) A_PlaySound("grenade/pickup",CHAN_WEAPON)
		TNT1 A 0 A_GiveInventory("DetonateCheck",1)
		DETV A 2 Offset(2,63)
		DETV A 2 Offset(2,84)
		TNT1 A 0 A_TakeInventory("DetonateCheck",1)
		Goto Ready
	Spawn:
		SATT A -1
		Loop
	}
}

Actor Satchel_Thrown : GrenadeBase
{
	Scale 0.3
	Mass 1
	Gravity 4
	Radius 4
	Height 3
	Speed 40
	Damage (random(4,12))
	DamageType "Frag"
	Reactiontime 8
	Projectile
	-NOGRAVITY
	-NOTELEPORT
	+WINDTHRUST
	BounceType "Doom"
	BounceFactor 0.4
	WallBounceFactor 0.6
	BounceSound "clusterbomb/debrisbounce"
	States
	{
	Spawn:
		SATT A 2
		SATT BCDEFGH 2
		TNT1 A 0 A_JumpIf(waterlevel == 3, "AdjustMass")
		Loop
	Swim:
		SATT A 2
		TNT1 A 0 A_JumpIfInTargetInventory("DetonateCheck",1,"Explosion")
		TNT1 A 0 A_ScaleVelocity(0.7) 
		TNT1 A 0 A_SpawnItemEx("Bubble", 0, 0, 0, 0, 0, 2, random (0, 360), 0, 128)
		SATT BCDEFGH 2
		Loop
	Death:
		SATT A 1
		TNT1 A 0 A_JumpIfInTargetInventory("DetonateCheck",1,"Explosion")
		Loop
	Explosion:
		EXP1 A 0 A_SpawnGroundSplash
		EXP1 A 0 A_SetScale(0.75,0.75)
		EXP1 A 0 A_AlertMonsters
		EXP1 A 0 A_StopSound(CHAN_VOICE)
		EXP1 A 0 A_StartSound("panzer/explode",CHAN_AUTO, 0, 1.0, 0.1)
		EXP1 A 0 A_Explode(492,700)
		EXP1 A 0 A_SpawnItemEx("ZScorch")
		TNT1 AAAAAAAAAAAAAAAAAAAAAAAAAAA 0 A_SpawnItemEx("TracerSpark_Longlive", 0, 0, 0, random(-5,5), random(-5,5), random(-5,5), random(0,359))
		TNT1 A 0 A_SpawnItemEx("Nuke",0,0,0)
		TNT1 A 8 A_SpawnItemEx("GeneralExplosion_Small",0,0,0)
		TNT1 A 16 A_SpawnItemEx("GeneralExplosion_Large",0,0,0)
		TNT1 A 2 BRIGHT LIGHT("BOAFLMW2") A_Quake(9,15,0,1024,"")
		TNT1 BCDEFGHIJKLMN 2 BRIGHT LIGHT("BOAFLMW2")
		Stop
	}
}

ACTOR DetonateCheck : Inventory
{
     Inventory.Amount 1
     Inventory.MaxAmount 1
     Inventory.Icon "TNT1A0"
     States
     {
     Spawn:
          TNT1 A -1
          Stop
          }
}