// I'm reminded of how the original PPSH sprites in Nazis! has the barrel missing from the gun. It's just the heatshield where the bullets come from. Clearly, it's superior Soviet engineering. Gun so good, it doesn't even need barrels.

ACTOR PPSH41Tracer : WaltherTracer
{
	Damage (random(11,12))
	Speed 100
}

ACTOR Tokarev762Casing : Casing9MM{}

ACTOR TokarevAmmo : Ammo
{
	Scale 0.4
	Tag "7.62x25mm"
	Inventory.Amount 142
	Inventory.MaxAmount 284
	Ammo.BackpackAmount 71
	Ammo.BackpackMaxAmount 213
	Inventory.PickupMessage "Picked up a drum of 7.62mm Tokarev ammo."
	Inventory.Icon PPSM01
	States
	{
	Spawn:
		PPSA A -1
		Stop
	}
}

ACTOR PPSH41 : NaziWeapon
{
	//$Category Weapons (BoA Addons)
	//$Title (4) PPSH
	//$Color 14
	Weapon.SlotNumber 4
	Scale .4
	Weapon.SelectionOrder 600
	Weapon.AmmoType "PPSH41Loaded"
	Weapon.AmmoUse 1
	weapon.ammotype2 "TokarevAmmo"
	weapon.ammouse2 1
	Weapon.AmmoGive1 71
	Weapon.AmmoGive2 142
	Tag "PPSh-41"
	Inventory.PickupMessage "You got the PPSh-41!"
	Weapon.UpSound "ppsh/select"
	States
	{
	Ready:
		PPSG A 0 A_JumpIfInventory("PPSH41Loaded",0,2)
		PPSG A 0 A_JumpIfInventory("TokarevAmmo",1,2)
		PPSG A 1 A_WeaponReady
		Loop
		PPSG A 1 A_WeaponReady(WRF_ALLOWRELOAD)
		Loop
	Deselect:
		PPSG A 0 A_Lower
		PPSG A 1 A_Lower
		Loop
	Select:
		PPSG A 0 A_Raise
		PPSG A 1 A_Raise
		Loop
	Fire:
		PPSG A 0 A_JumpIfInventory("PPSH41Loaded",1,1)
		Goto Dryfire
		PPSG A 0 A_GunFlash
		TNT1 A 0 
		PPSG A 0 A_PlaySound("ppsh/fire",CHAN_WEAPON)
		PPSG A 0 A_SpawnItemEx("Tokarev762Casing",12,-20,32,8,random(-2,2),random(0,4),random(-55,-80),SXF_NOCHECKPOSITION)
		PPSG A 0 A_AlertMonsters
		PPSG A 1 A_FireCustomMissile("PPSH41Tracer",frandom(-2.5,2.5),1,0,0,0,frandom(-1.0,1.0))
		PPSG D 1 Offset(0,40) A_SetPitch(pitch-(0.6*boa_recoilamount))
		PPSG D 0 A_JumpIf(waterlevel > 0,2)
		PPSG D 0 A_FireProjectile("ChainSmokeSpawner",0,0,0,random(-4,4),0,0)
		PPSG A 1 Offset(0,43) A_Refire
		PPSG A 1 Offset(0,42) A_WeaponReady(WRF_NOBOB)
		PPSG A 1 Offset(0,44) A_WeaponReady(WRF_NOBOB)
		PPSG A 1 Offset(0,43) A_WeaponReady(WRF_NOBOB)
		PPSG A 1 Offset(0,40) A_WeaponReady(WRF_NOBOB)
		PPSG A 1 Offset(0,36) A_WeaponReady(WRF_NOBOB)
		PPSG A 1 Offset(0,32) A_WeaponReady(WRF_NOBOB)
		Goto Ready
	Hold:
		PPSG A 0 A_JumpIfInventory("PPSH41Loaded",1,1)
		Goto Dryfire
		PPSG A 0 A_GunFlash
		TNT1 A 0 
		PPSG A 0 A_PlaySound("ppsh/fire",CHAN_WEAPON)
		PPSG A 0 A_SpawnItemEx("Tokarev762Casing",12,-20,32,8,random(-2,2),random(0,4),random(-55,-80),SXF_NOCHECKPOSITION)
		PPSG A 0 A_AlertMonsters
		PPSG A 1 A_FireCustomMissile("PPSH41Tracer",frandom(-2.5,2.5),1,0,0,0,frandom(-1.0,1.0))
		PPSG D 1 Offset(0,52) A_SetPitch(pitch-(0.8*boa_recoilamount))
		PPSG D 0 A_JumpIf(waterlevel > 0,2)
		PPSG D 0 A_FireProjectile("ChainSmokeSpawner",0,0,0,random(-4,4),0,0)
		PPSG A 1 Offset(0,53) A_Refire
		PPSG A 1 Offset(0,52) A_WeaponReady(WRF_NOBOB)
		PPSG A 1 Offset(0,54) A_WeaponReady(WRF_NOBOB)
		PPSG A 1 Offset(0,53) A_WeaponReady(WRF_NOBOB)
		PPSG A 1 Offset(0,50) A_WeaponReady(WRF_NOBOB)
		PPSG A 1 Offset(0,46) A_WeaponReady(WRF_NOBOB)
		PPSG A 1 Offset(0,41) A_WeaponReady(WRF_NOBOB)
		PPSG A 1 Offset(0,35) A_WeaponReady(WRF_NOBOB)
		PPSG A 1 Offset(0,32) A_WeaponReady(WRF_NOBOB)
		Goto Ready
	Reload:
		TNT1 A 0 
		PPSG F 1 Offset(0,35) A_PlaySound("ppsh/reload",5)
		PPSG F 1 Offset(0,38)
		PPSG F 1 Offset(0,44)
		PPSG B 1 Offset(0,52)
		PPSG B 1 Offset(-1,54)
		PPSG B 1 Offset(-2,56)
		PPSG B 1 Offset(-3,58)
		PPSG B 1 Offset(-4,58)
		PPSG B 1 Offset(-4,57)
		PPSG B 1 Offset(-3,54)
		PPSG B 1 Offset(-3,51)
		PPSG B 1 Offset(-3,53)
		PPSG B 1 Offset(-3,55)
		PPSG B 1 Offset(-3,56)
	ReloadLoop:
		TNT1 A 0 A_TakeInventory("TokarevAmmo",1,TIF_NOTAKEINFINITE)
		TNT1 A 0 A_GiveInventory("PPSH41Loaded")
		TNT1 A 0 A_JumpIfInventory("PPSH41Loaded",0,"ReloadFinish")
		TNT1 A 0 A_JumpIfInventory("TokarevAmmo",1,"ReloadLoop")
	ReloadFinish:
		PPSG F 1 Offset(-3,57)
		PPSG F 1 Offset(-3,59)
		PPSG F 1 Offset(-3,63)
		PPSG F 1 Offset(-3,67)
		PPSG C 1 Offset(-3,65)
		PPSG C 1 Offset(-3,62)
		PPSG C 1 Offset(-3,58)
		PPSG C 1 Offset(-3,55)
		PPSG C 1 Offset(-2,53)
		PPSG C 1 Offset(-2,49)
		PPSG C 1 Offset(-2,46)
		PPSG C 1 Offset(-2,45)
		PPSG C 1 Offset(-1,44)
		PPSG C 1 Offset(-1,46)
		PPSG C 1 Offset(-1,47)
		PPSG A 1 Offset(0,49) A_WeaponReady(WRF_NOBOB)
		PPSG A 1 Offset(0,44) A_WeaponReady(WRF_NOBOB)
		PPSG A 1 Offset(0,38) A_WeaponReady(WRF_NOBOB)
		PPSG A 1 Offset(0,35) A_WeaponReady(WRF_NOBOB)
		PPSG A 1 Offset(0,32) A_WeaponReady(WRF_NOBOB)
		Goto Ready
	Flash:
		PPSF A 1 BRIGHT A_Light2
		PPSF B 1 BRIGHT A_Light1
		TNT1 A 1
		Goto LightDone
	Spawn:
		PPSH A -1
		Stop
	}
}

ACTOR PPSH41Loaded : Ammo
{
	Tag "7.62x25mm"
	+IGNORESKILL
	Inventory.MaxAmount 71
	Inventory.Icon PPSM03
}