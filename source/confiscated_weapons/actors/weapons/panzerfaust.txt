// There was initially a whole different sprite set used for the Panzerfaust. I had used modelshots from a Sven Coop Afrikakorps reskin pack that didn't look too bad, and it was fine in implementation. Later, I found another Panzerfaust model that I thought looked better, so I decided to modelshot that one and replace the first iteration of the Faust with the current one. Who knows, I might reuse the first iteration sprites incase another project shows up that need panzerfaust sprites again.

ACTOR PanzerfaustCasing : Casing9mm
{
	Scale .3
	States
	{
	Spawn:
		PNFC A 2 NODELAY A_JumpIf(waterlevel == 3, "AdjustMass") //mxd
		Loop
	AdjustMass: //mxd
		PNFC A 0 A_SetMass(400)
		Goto Swim
	Swim: //mxd. This presumes all cases have the same ammout of frames...
		PNFC A 2 A_ScaleVelocity(0.7)
		PNFC A 2 A_SpawnItemEx("Bubble", 0, 0, 0, 0, 0, 2, random (0, 360), 0, 128)
		PNFC A 2 A_ScaleVelocity(0.7)
		PNFC A 2 A_SpawnItemEx("Bubble", 0, 0, 0, 0, 0, 2, random (0, 360), 0, 128)
		PNFC A 2 A_ScaleVelocity(0.7)
		PNFC A 2 A_SpawnItemEx("Bubble", 0, 0, 0, 0, 0, 2, random (0, 360), 0, 128)
		PNFC A 2 A_ScaleVelocity(0.7)
		PNFC A 2 A_SpawnItemEx("Bubble", 0, 0, 0, 0, 0, 2, random (0, 360), 0, 128)
	SwimLoop: // Don't spawn more bubbles after the first batch
		PNFC A 2
		Loop
	Death:
		PNFC A 0 A_UnSetSolid
		PNFC A 0 A_Jump(256,"Death1")
		Stop
	}
}

ACTOR PanzerfaustAmmo : Ammo
{
	Scale 0.3
	Tag "Panzerfaust Rocket"
	Inventory.Amount 1
	Inventory.MaxAmount 2
	Ammo.BackpackAmount 1
	Ammo.BackpackMaxAmount 4
	Inventory.PickupMessage "Picked up a rocket for the Panzerfaust."
	Inventory.Icon PSFA01
	States
	{
	Spawn:
		PNSS A -1
		Stop
	}
}

ACTOR Panzerfaust : NaziWeapon
{
	Weapon.SlotNumber 7
	Weapon.SelectionOrder 10000
	Inventory.PickupMessage "You got the Panzerfaust!"
	Tag "Panzerfaust"
	Weapon.AmmoType "PanzerfaustLoaded"
	Weapon.AmmoUse 1
	Weapon.AmmoType2 "PanzerfaustAmmo"
	Weapon.AmmoUse2 1
	Weapon.AmmoGive1 1
	Weapon.AmmoGive2 2
	+WEAPON.NOAUTOFIRE
	Scale .19
	Weapon.UpSound "panzerfaust/select"
	States
	{
	Select:
		TNT1 A 0 A_JumpIfInventory("PanzerfaustLoaded",1,3)
		PNFS C 0 A_Raise
		PNFS C 1 A_Raise
		Loop
		PNFS A 0 A_Raise
		PNFS A 1 A_Raise
		Loop
	Deselect:
		TNT1 A 0 A_JumpIfInventory("PanzerfaustLoaded",1,3)
		PNFS C 0 A_Lower
		PNFS C 1 A_Lower
		Loop
		PNFS A 0 A_Lower
		PNFS A 1 A_Lower
		Loop
	Ready:
		TNT1 A 0 A_JumpIfInventory("PanzerfaustLoaded",1,2)
		Goto ReadyEmpty
		TNT1 A 0 A_CheckReload
		PNFS A 0 A_JumpIfInventory("PanzerfaustLoaded",0,2)
		PNFS A 0 A_JumpIfInventory("PanzerfaustAmmo",1,2)
		PNFS A 1 A_WeaponReady
		Loop
		PNFS A 1 A_WeaponReady(WRF_ALLOWRELOAD)
		Loop
	ReadyEmpty:
		TNT1 A 0 A_CheckReload
		TNT1 A 0 A_JumpIfInventory("PanzerfaustAmmo",1,2)
		PNFS C 1 A_WeaponReady
		Goto Ready
		PNFS C 1 A_WeaponReady(WRF_ALLOWRELOAD)
		Goto Ready
	Fire:
		PNFS A 0 A_JumpIfInventory("PanzerfaustLoaded",1,1)
		Goto Dryfire
		PNFS A 0 A_PlaySound("panzerfaust/fire",CHAN_WEAPON)
		PNFS A 0 A_GunFlash
		PNFS B 2 Bright A_FireCustomMissile("PanzerfaustRocket",0,1,0,0)
		PNFS B 0 A_JumpIf(waterlevel > 0,2)
		PNFS B 0 A_FireProjectile("ShotSmokeSpawner",0,0,0,random(-4,4),0,0)
		PNFS B 1 Offset(6,34)
		PNFS B 1 Offset(1,36)
		PNFS B 1 Offset(2,39)
		PNFS B 1 Offset(2,43)
		PNFS B 1 Offset(3,48)
		PNFS B 1 Offset(2,44)
		PNFS B 1 Offset(2,40)
		PNFS B 1 Offset(1,36)
		PNFS B 1 Offset(1,34)
		PNFS B 1 Offset(6,32)
		PNFS B 1 Offset(3,30)
		PNFS C 1 Offset(1,29)
		PNFS C 1 Offset(-2,30)
		PNFS C 1 Offset(-1,32)
		TNT1 A 0 A_CheckReload
		Goto Ready
	Reload:
		PNFS C 1 Offset(1,33) 
		PNFS C 1 Offset(2,34)
		PNFS C 1 Offset(3,35)
		PNFS C 1 Offset(4,36)
		PNFS C 1 Offset(4,43)
		PNFS C 1 Offset(4,50)
		PNFS C 1 Offset(5,58)
		PNFS C 1 Offset(5,67)
		PNFS C 1 Offset(5,71)
		PNFS C 1 Offset(5,77)
		PNFS C 1 Offset(4,83)
		PNFS C 1 Offset(3,89)
		PNFS C 1 Offset(3,94)
		PNFS C 1 Offset(2,100)
		PNFS C 1 Offset(2,102)
		PNFS C 1 Offset(1,104)
		PNFS C 1 Offset(1,106)
		PNFS C 1 Offset(-1,108)
		TNT1 A 1 Offset(-3,112) A_SpawnItemEx("PanzerfaustCasing",12,-20,32,8,random(-2,2),random(0,4),random(-55,-80),SXF_NOCHECKPOSITION)
		TNT1 A 1 Offset(-5,116)
		TNT1 A 1 Offset(-7,120)
		TNT1 A 1 Offset(-10,124)
		TNT1 A 1 Offset(-14,126)
		TNT1 A 1 Offset(-18,128)
		PNFS A 0 A_TakeInventory("PanzerfaustAmmo",1)
		PNFS A 1 Offset(-21,129) A_GiveInventory("PanzerfaustLoaded")
		PNFS A 1 Offset(-24,130) A_PlaySound("panzerfaust/load",CHAN_WEAPON)
		PNFS A 1 Offset(-27,131)
		PNFS A 1 Offset(-28,130)
		PNFS A 1 Offset(-26,128)
		PNFS A 1 Offset(-22,124)
		PNFS A 1 Offset(-21,112)
		PNFS A 1 Offset(-20,108)
		PNFS A 1 Offset(-19,98)
		PNFS A 1 Offset(-18,88)
		PNFS A 1 Offset(-17,78)
		PNFS A 1 Offset(-16,68)
		PNFS A 1 Offset(-15,57)
		PNFS A 1 Offset(-14,46)
		PNFS A 1 Offset(-12,42)
		PNFS A 1 Offset(-11,38)
		PNFS A 1 Offset(-8,35)
		PNFS A 1 Offset(-6,32)
		PNFS A 1 Offset(-2,28)
		PNFS A 1 Offset(1,27)
		PNFS A 1 Offset(-1,29)
		Goto Ready
	Spawn:
		PNFW A -1
		Loop
	}
}

ACTOR PanzerfaustLoaded : Ammo
{
	+IGNORESKILL
	Tag "Loaded"
	Inventory.Icon PSFA01
}

ACTOR PanzerfaustRocket : GrenadeBase
{
	Scale 0.19
	Radius 5
	Height 6
	Speed 70
	PROJECTILE
	-NOGRAVITY
	-NOTELEPORT
	+WINDTHRUST
	Damage (1000)
	Decal Scorch
	DamageType "Rocket"
	Obituary "%o notices %k's rocket speeding towards %h."
	ProjectileKickback 25000
	GrenadeBase.SplashType "Missile"
	States
	{
	Spawn:
		PNSS A 1 BRIGHT NODELAY
		PNSS A 1 BRIGHT
		Wait
	Death:
		EXP1 A 0 A_SpawnGroundSplash
		EXP1 A 0 A_SetScale(0.75,0.75)
		EXP1 A 0 A_AlertMonsters
		EXP1 A 0 A_StopSound(CHAN_VOICE)
		EXP1 A 0 A_StartSound("grenade/explode",CHAN_AUTO, 0, 1.0, 0.1)
		EXP1 A 0 A_Explode(192)
		EXP1 A 0 A_SpawnItemEx("ZScorch")
		TNT1 AAAAAAAAAAAAAAAAAAAAAAAAAAA 0 A_SpawnItemEx("TracerSpark_Longlive", 0, 0, 0, random(-5,5), random(-5,5), random(-5,5), random(0,359)) //T667 improvements
		TNT1 A 8 A_SpawnItemEx("GeneralExplosion_Medium",0,0,0)
		TNT1 A 2 BRIGHT LIGHT("BOAFLMW2") A_Quake(9,15,0,1024,"")
		TNT1 BCDEFGHIJKLMN 2 BRIGHT LIGHT("BOAFLMW2")
		Stop
	}
}