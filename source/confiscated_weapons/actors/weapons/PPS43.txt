// I remember using the same model to modelshot the SMG for my HDest PA reskin set. Why I bring up this particular history is because I'm so used to the thing having 30 rounds, I had to be corrected about its IRL magazine capacity. How silly of me.

ACTOR PPS43Tracer : PPSH41Tracer
{
	Speed 90
}

ACTOR PPS43 : NaziWeapon
{
	//$Category Weapons (BoA Addons)
	//$Title (4) PPS43
	//$Color 14
	Weapon.SlotNumber 4
	Weapon.SelectionOrder 600
	Inventory.PickupMessage "You got the PPS43!"
	Weapon.AmmoType "PPS43Loaded"
	Weapon.AmmoUse 1
	Weapon.AmmoType2 "TokarevAmmo"
	Weapon.AmmoUse2 1
	Weapon.AmmoGive1 35
	Weapon.AmmoGive2 70
	Scale 0.5
	Tag "PPS-43"
	Weapon.UpSound "pipis/select"
	States
	{
	Ready:
		PPS4 A 0 A_JumpIfInventory("PPS43Loaded",0,2)
		PPS4 A 0 A_JumpIfInventory("TokarevAmmo",1,2)
		PPS4 A 1 A_WeaponReady
		Loop
		PPS4 A 1 A_WeaponReady(WRF_ALLOWRELOAD)
		Loop
	Deselect:
		PPS4 A 0 A_Lower
		PPS4 A 1 A_Lower
		Loop
	Select:
		PPS4 A 0 A_Raise
		PPS4 A 1 A_Raise
		Loop
	Fire:
		PPS4 A 0 A_JumpIfInventory("PPS43Loaded",1,1)
		Goto Dryfire
		PPS4 A 0 A_GunFlash
		PPS4 A 0 A_PlaySound("pipis/fire",CHAN_WEAPON)
		PPS4 A 0 A_SpawnItemEx("45ACPCasing",12,-20,32,8,random(-2,2),random(0,4),random(-55,-80),SXF_NOCHECKPOSITION)
		PPS4 A 0 A_AlertMonsters
		PPS4 D 1 A_FireCustomMissile("PPS43Tracer",frandom(-1.5,1.5),1,0,0,0,frandom(-1.0,1.0))
		PPS4 D 1 Offset(0,40) A_SetPitch(pitch-(0.6*boa_recoilamount))
		PPS4 A 0 A_JumpIf(waterlevel > 0,2)
		PPS4 A 0 A_FireProjectile("ChainSmokeSpawner",0,0,0,random(-4,4),0,0)
		PPS4 A 1 Offset(0,34)
		TNT1 A 0 A_Refire
		Goto Ready
	Hold:
		PPS4 A 0 A_JumpIfInventory("PPS43Loaded",1,1)
		Goto Dryfire
		PPS4 A 0 A_GunFlash
		PPS4 A 0 A_PlaySound("pipis/fire",CHAN_WEAPON)
		PPS4 A 0 A_SpawnItemEx("45ACPCasing",12,-20,32,8,random(-2,2),random(0,4),random(-55,-80),SXF_NOCHECKPOSITION)
		PPS4 A 0 A_AlertMonsters
		PPS4 D 1 Offset(0,36) A_FireCustomMissile("PPS43Tracer",frandom(-1.5,1.5),1,0,0,0,frandom(-1.0,1.0))
		PPS4 D 1 Offset(0,40) A_SetPitch(pitch-(0.6*boa_recoilamount))
		PPS4 A 0 A_JumpIf(waterlevel > 0,2)
		PPS4 A 0 A_FireProjectile("ChainSmokeSpawner",0,0,0,random(-4,4),0,0)
		PPS4 A 1 Offset(0,34)
		TNT1 A 0 A_Refire
		Goto Ready
	Reload:
		PPS4 A 1 Offset(0,35) A_PlaySound("pipis/reload",5)
		PPS4 A 1 Offset(0,38) 
		PPS4 A 1 Offset(0,44)
		PPS4 A 1 Offset(-2,46)
		PPS4 E 1 Offset(-4,52)
		PPS4 E 1 Offset(-7,56)
		PPS4 E 1 Offset(-10,57)
		PPS4 E 8 Offset(-11,58)
	ReloadLoop:
		TNT1 A 0 A_TakeInventory("TokarevAmmo",1,TIF_NOTAKEINFINITE)
		TNT1 A 0 A_GiveInventory("PPS43Loaded")
		TNT1 A 0 A_JumpIfInventory("PPS43Loaded",0,"ReloadFinish")
		TNT1 A 0 A_JumpIfInventory("TokarevAmmo",1,"ReloadLoop")
	ReloadFinish:
		PPS4 A 1 Offset(-10,58)
		PPS4 A 1 Offset(-10,61)
		PPS4 F 1 Offset(-10,65)
		PPS4 F 1 Offset(-10,71)
		PPS4 F 1 Offset(-10,65)
		PPS4 F 1 Offset(-10,60)
		PPS4 F 1 Offset(-10,55)
		PPS4 F 1 Offset(-10,53)
		PPS4 F 1 Offset(-10,51)
		PPS4 F 1 Offset(-9,50)
		PPS4 A 1 Offset(-8,46)
		PPS4 A 1 Offset(-7,43)
		PPS4 A 1 Offset(-5,40)
		PPS4 A 1 Offset(-3,37)
		PPS4 A 1 Offset(-1,34)
		PPS4 A 1 Offset(0,32)
		Goto Ready
	Flash:
		PPS4 B 1 BRIGHT A_Light2
		PPS4 C 1 BRIGHT
		TNT1 A 2 A_Light1
		Goto LightDone
	Spawn:
		PPS4 P -1
		Stop
	}
}

ACTOR PPS43Loaded : Ammo
{
	Tag "7.62x25mm"
	Inventory.MaxAmount 35
	+IGNORESKILL
	Inventory.Icon NAMA03
}