// Twiddled my thumbs about adding slugs to this category. But then it would be just a rifle with an even rarer ammo type. Decided then to instead close up the spread on the regular shells instead. A good long range option compared to the likes of the trenchie, I feel.

ACTOR Ithaca37 : NaziWeapon
{
	//$Category Weapons (Confiscated Weapons)
	//$Title (3) Ithaca 37
	//$Color 14
	Weapon.SlotNumber 3
	Weapon.SelectionOrder 400
	Weapon.AmmoType "IthacaShotgunLoaded"
	Weapon.AmmoUse 1
	Weapon.AmmoType2 "Ammo12Gauge"
	Weapon.AmmoUse2 1
	Inventory.PickupMessage "You got the Ithaca 37!"
	+WEAPON.NOAUTOFIRE
	Weapon.AmmoGive1 4
	Weapon.AmmoGive2 8
	Scale .3
	Tag "Ithaca 37"
	Weapon.UpSound "ithy/select"
	States
	{
	Select:
		IC37 A 0 A_Raise
		IC37 A 1 A_Raise
		Loop
	Deselect:
		IC37 A 0 A_JumpIfInventory("IthacaReloading",1,3)
		IC37 A 0 A_Lower
		IC37 A 1 A_Lower
		Loop
		IC37 C 1 Offset(3,35) A_PlaySound("ithy/pump")
		IC37 C 1 Offset(-1,38)
		IC37 D 1 Offset(-4,41)
		IC37 D 1 Offset(-1,33)
		IC37 E 1 Offset(-2,37)
		IC37 E 1 Offset(-3,43)
		IC37 D 1 Offset(-2,40)
		IC37 D 1 Offset(-4,41)
		IC37 C 1 Offset(-1,38)
		IC37 C 1 Offset(3,35)
		IC37 B 1 Offset(-1,38)
		IC37 B 1 Offset(2,36)
		IC37 B 1 Offset(5,34) A_TakeInventory("IthacaReloading",1)
		Loop
	Ready:
		IC37 A 0 A_JumpIfInventory("IthacaShotgunLoaded",0,2)
		IC37 A 0 A_JumpIfInventory("Ammo12Gauge",1,2)
		IC37 A 1 A_WeaponReady
		Loop
		IC37 A 1 A_WeaponReady(WRF_ALLOWRELOAD)
		Loop
	Fire:
		IC37 A 0 A_JumpIfInventory("IthacaReloading",1,"ReloadFinish")
		IC37 A 0 A_JumpIfInventory("IthacaShotgunLoaded",1,1)
		Goto Dryfire
		IC37 A 0 A_TakeInventory("IthacaShotgunLoaded",1)
		IC37 A 0 A_PlaySound("ithy/fire",CHAN_WEAPON)
		IC37 A 0 A_AlertMonsters
		IC37 AAAAAAAAAA 0 A_FireCustomMissile("ShotgunTracer",frandom(-0.5,0.5),0,0,0,0,frandom(-0.5,0.5))
		IC37 F 1 A_GunFlash
		IC37 F 1 A_SetPitch(pitch-(4.0*boa_recoilamount))
		IC37 F 1 Offset(0,40) A_SetPitch(pitch-(2.0*boa_recoilamount))
		IC37 F 1 Offset(0,36) A_SetPitch(pitch-(1.0*boa_recoilamount))
		IC37 F 1 Offset(0,34) A_SetPitch(pitch+(1.0*boa_recoilamount))
		IC37 F 1 Offset(0,33) A_SetPitch(pitch+(0.5*boa_recoilamount))
		IC37 F 7 A_WeaponReady(WRF_NOFIRE)
		IC37 F 1 Offset(-1,33)
		IC37 B 1 Offset(5,34) A_PlaySound("ithy/pump",5)
		IC37 B 1 Offset(2,36)
		IC37 B 1 Offset(-1,38)
		IC37 C 1 Offset(3,35)
		IC37 C 1 Offset(-1,38)
		IC37 D 1 Offset(-4,41)
		IC37 D 1 Offset(-4,43) A_SpawnItemEx("ShotgunCasing",12,-20,32,8,random(-2,2),random(0,4),random(-55,-80),SXF_NOCHECKPOSITION)
		IC37 E 1 Offset(-4,45)
		IC37 E 1 Offset(-3,44)
		IC37 D 1 Offset(-2,40)
		IC37 D 1 Offset(-4,41)
		IC37 C 1 Offset(-1,38)
		IC37 C 1 Offset(-1,37)
		IC37 B 1 Offset(1,36)
		IC37 B 1 Offset(2,36)
		IC37 BA 1 Offset(5,34)
		TNT1 A 0 A_CheckReload
		Goto Ready
	Reload:
		IC37 A 1 Offset(-1,33) A_GiveInventory("IthacaReloading")
		IC37 B 1 Offset(5,34)
		IC37 B 1 Offset(2,36)
		IC37 B 1 Offset(-1,38)
		IC37 C 1 Offset(3,35)
		IC37 C 1 Offset(-1,38)
		IC37 C 1 Offset(-4,41)
		IC37 C 1 Offset(-6,46)
		IC37 C 1 Offset(-7,55)
	ReloadLoop:
		IC37 A 0 A_TakeInventory("Ammo12Gauge",1,TIF_NOTAKEINFINITE)
		IC37 A 0 A_GiveInventory("IthacaShotgunLoaded")
		IC37 C 1 Offset(-8,58) A_PlaySound("ithy/load",CHAN_WEAPON)
		IC37 C 1 Offset(-9,64)
		IC37 C 1 Offset(-10,70)
		IC37 C 1 Offset(-10,68)
		IC37 C 1 Offset(-9,66)
		IC37 C 1 Offset(-9,64)
		IC37 C 1 Offset(-9,62)
		IC37 C 1 Offset(-8,61) A_WeaponReady(WRF_NOBOB|WRF_NOSECONDARY)
		IC37 C 1 Offset(-8,60) A_WeaponReady(WRF_NOBOB|WRF_NOSECONDARY)
		IC37 C 2 Offset(-8,59) A_WeaponReady(WRF_NOBOB|WRF_NOSECONDARY)
		IC37 C 3 Offset(-8,58) A_WeaponReady(WRF_NOBOB|WRF_NOSECONDARY)
		IC37 A 0 A_JumpIfInventory("IthacaShotgunLoaded",0,"ReloadFinish")
		TNT1 A 0 A_JumpIfInventory("Ammo12Gauge",1,"ReloadLoop")
	ReloadFinish:
		IC37 C 1 Offset(3,35) A_PlaySound("ithy/pump")
		IC37 C 1 Offset(-1,38)
		IC37 D 1 Offset(-4,41)
		IC37 D 1 Offset(-1,33)
		IC37 E 1 Offset(-2,37)
		IC37 E 1 Offset(-3,43)
		IC37 D 1 Offset(-2,40)
		IC37 D 1 Offset(-4,41)
		IC37 C 1 Offset(-1,38)
		IC37 C 1 Offset(3,35)
		IC37 B 1 Offset(-1,38)
		IC37 B 1 Offset(2,36)
		IC37 B 1 Offset(5,34) A_TakeInventory("IthacaReloading",1)
		Goto Ready
	Flash:
		I37F A 1 BRIGHT A_Light2
		TNT1 A 1
		TNT1 A 2 A_Light1
		Goto LightDone
	Spawn:
		I37W A -1
		Stop
	}
}

ACTOR IthacaShotgunLoaded : Ammo
{
	Tag "12 Gauge"
	+IGNORESKILL
	Inventory.MaxAmount 4
	Inventory.Icon BROW01
}

ACTOR IthacaReloading : Inventory{}