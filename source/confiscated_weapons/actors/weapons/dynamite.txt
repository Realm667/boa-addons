// I thought about adding a firemode where you can set the timer on this thing, just like in RTCW. However, this was in the 40-to-50 range, and I was definitely running out of steam around this thing's implementation. Decided to leave in a quick and easy 5 second timer.

ACTOR DynamiteAmmo : Ammo
{
	Scale 0.25
	Tag "Components of Dynamite"
	Inventory.Amount 1
	Inventory.MaxAmount 2
	Ammo.BackpackAmount 0
	Ammo.BackpackMaxAmount 4
	Inventory.PickupMessage "Someway, somehow, you managed to cobble up a whole new dynamite out of spare parts while looking at the dynamites you already have. Also, you're not actually supposed to see this, but I like to have fun with how people might handwave getting ammo for a weapon like this."
	Inventory.Icon DYNT01
	States
	{
	Spawn:
		DYNW A -1
		Stop
	}
}

ACTOR Dynamite : NaziWeapon
{
	//$Category Weapons (Confiscated Weapons)
	//$Title (9) Trinitrotoluene
	// Hah, I got it right the first time! TNT is easy to say, but I wanted to challenge myself with typing out the full name without looking it up (though I did end up looking it up anyways to check if I was right or not, pfft). Nice to see I did.
	//$Color 14
	Weapon.SlotNumber 9
	Weapon.SelectionOrder 10000
	Inventory.PickupMessage "You got a Dynamite! The timer's fixed to 5 seconds."
	Tag "Dynamite"
	Weapon.AmmoUse 1
	Weapon.AmmoType "DynamiteAmmo"
	Weapon.AmmoGive 2
	-WEAPON.AMMO_CHECKBOTH
	-WEAPON.NOALERT
	Scale 0.25
	Weapon.UpSound "grenade/select"
	Inventory.PickupSound "grenade/pickup"
	States
	{
	Select:
		DYNV A 0 A_Raise
		DYNV A 1 A_Raise
		Loop
	Deselect:
		TNT1 A 0 A_JumpIfInventory("DynamiteAmmo",1,3)
		TNT1 A 0 A_Lower
		TNT1 A 1 A_Lower
		Loop
		DYNV A 0 A_Lower
		DYNV A 1 A_Lower
		Loop
	Ready:
		TNT1 A 0 A_JumpIfInventory("DynamiteAmmo",1,2)
		TNT1 A 1 A_WeaponReady
		Loop
		DYNV A 1 A_WeaponReady
		Loop
	Fire:
		DYNV B 1 Offset(6,34)
		DYNV B 1 Offset(1,46)
		DYNV B 1 Offset(2,59)
		DYNV B 1 Offset(2,63)
		DYNV B 1 Offset(3,78)
		DYNV B 1 Offset(2,84)
		DYNV B 1 Offset(2,90) A_PlaySound("weapon/dryfire",CHAN_WEAPON)
		DYNV B 1 Offset(1,106) 
		DYNV B 1 Offset(1,114)
		DYNV B 1 Offset(6,122)
		DYNV B 1 Offset(3,130)
		TNT1 A 0 A_PlaySound("weapons/thrownoclick",CHAN_WEAPON)
		TNT1 A 2 Bright A_FireCustomMissile("Dynamite_Thrown",0,1,0,0,0,0)
		TNT1 A 1 Offset(1,29)
		TNT1 A 1 Offset(-2,30)
		TNT1 A 1 Offset(-1,32)
		Goto Ready
	Spawn:
		DYNW A -1
		Loop
	}
}

Actor Dynamite_Thrown : GrenadeBase
{
	Scale 0.25
	Mass 10
	Gravity 4
	Radius 4
	Height 3
	Speed 40
	Damage (random(4,12))
	DamageType "Frag"
	Reactiontime 8
	Projectile
	-NOGRAVITY
	-NOTELEPORT
	+WINDTHRUST
	BounceType "Doom"
	BounceFactor 0.4
	WallBounceFactor 0.6
	BounceSound "clusterbomb/debrisbounce"
	States
	{
	Spawn:
		DYNT A 2 A_CountDown
		DYNT BCDEFGH 2
		TNT1 A 0 A_JumpIf(waterlevel == 3, "AdjustMass")
		Loop
	Swim:
		DYNT A 2 A_CountDown() 
		TNT1 A 0 A_ScaleVelocity(0.7) 
		TNT1 A 0 A_SpawnItemEx("Bubble", 0, 0, 0, 0, 0, 2, random (0, 360), 0, 128)
		DYNT BCDEFGH 2
		Loop
	Death:
		DYNT A 35 A_StartSound("world/bomb_ticker",CHAN_AUTO, 0, 1.0, 0.1)
		DYNT A 35 A_StartSound("world/bomb_ticker",CHAN_AUTO, 0, 1.0, 0.1)
		DYNT A 35 A_StartSound("world/bomb_ticker",CHAN_AUTO, 0, 1.0, 0.1)
		DYNT A 35 A_StartSound("world/bomb_ticker",CHAN_AUTO, 0, 1.0, 0.1)
		DYNT A 35 A_StartSound("world/bomb_ticker",CHAN_AUTO, 0, 1.0, 0.1)
		EXP1 A 0 A_SpawnGroundSplash
		EXP1 A 0 A_SetScale(0.75,0.75)
		EXP1 A 0 A_AlertMonsters
		EXP1 A 0 A_StopSound(CHAN_VOICE)
		EXP1 A 0 A_StartSound("panzer/explode",CHAN_AUTO, 0, 1.0, 0.1)
		EXP1 A 0 A_Explode(492,700)
		EXP1 A 0 A_SpawnItemEx("ZScorch")
		TNT1 AAAAAAAAAAAAAAAAAAAAAAAAAAA 0 A_SpawnItemEx("TracerSpark_Longlive", 0, 0, 0, random(-5,5), random(-5,5), random(-5,5), random(0,359))
		TNT1 A 0 A_SpawnItemEx("Nuke",0,0,0)
		TNT1 A 8 A_SpawnItemEx("GeneralExplosion_Small",0,0,0)
		TNT1 A 16 A_SpawnItemEx("GeneralExplosion_Large",0,0,0)
		TNT1 A 2 BRIGHT LIGHT("BOAFLMW2") A_Quake(9,15,0,1024,"")
		TNT1 BCDEFGHIJKLMN 2 BRIGHT LIGHT("BOAFLMW2")
		Stop
	}
}