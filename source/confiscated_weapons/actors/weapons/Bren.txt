// There was a point in time where during the modelshot process I considered to put the viewmodel dead smack in the center. I wonder how useless that would've made the gun. Sure, firepower's great, but it's no good when you can't aim the blasted thing, innit?

ACTOR 303Ammo : Ammo
{
	Tag ".303 British"
	Inventory.Amount 20
	Inventory.MaxAmount 40
	Ammo.BackpackAmount 30
	Ammo.BackpackMaxAmount 100
	Inventory.PickupMessage "Picked up a .303 charger clip."
	Inventory.Icon 303R01
	Scale 0.5
	States
	{
	Spawn:
		303A A -1
		Stop
	}
}

ACTOR 303AmmoBox : 303Ammo
{
	Scale 0.6
	Inventory.Amount 40
	Inventory.PickupMessage "Picked up a box of .303 British ammo."
	States
	{
	Spawn:
		303A A -1
		Stop
	}
}


ACTOR BrenfieldTracer : PlayerTracer
{
	Damage (random(40,65))
	Speed 180
}

ACTOR Bren : NaziWeapon
{
	//$Category Weapons (Confiscated Weapons)
	//$Title (8) Bren LMG
	//$Color 14
	Scale 0.5
	Weapon.SlotNumber 8
	Weapon.SelectionOrder 750
	Weapon.AmmoType "BrenLoaded"
	Weapon.AmmoUse 1
	Weapon.AmmoType2 "303Ammo"
	Weapon.AmmoUse2 1
	Weapon.AmmoGive1 30
	Weapon.AmmoGive2 30
	Tag "Bren LMG"
	Inventory.PickupMessage "You got the Bren LMG!"
	Weapon.UpSound "bren/select"
	States
	{
	Select:
		BREN A 0 A_Raise
		BREN A 1 A_Raise
		Loop
	Deselect:
		BREN A 0 A_Lower
		BREN A 1 A_Lower
		Loop
	Ready:
		BREN A 0 A_JumpIfInventory("BrenLoaded",0,2)
		BREN A 0 A_JumpIfInventory("303Ammo",1,2)
		BREN A 1 A_WeaponReady
		Loop
		BREN A 1 A_WeaponReady(WRF_ALLOWRELOAD)
		Loop
	Fire:
		BREN A 0 A_JumpIfInventory("BrenLoaded",1,1)
		Goto Dryfire
		BREN A 0 A_GunFlash
		BREN A 0 A_PlaySound("bren/fire",CHAN_WEAPON)
		BREN A 0 A_SpawnItemEx("EnfieldRifleCasing",12,-20,32,8,random(-2,2),random(0,4),random(-55,-80),SXF_NOCHECKPOSITION)
		BREN A 0 A_AlertMonsters
		BREN A 1 A_FireCustomMissile("BrenfieldTracer",frandom(-0.6,0.6))
		BREN A 0 A_JumpIf(waterlevel > 0,2)
		BREN A 0 A_FireProjectile("ChainSmokeSpawner",0,0,0,random(-4,4),0,0)
		BREN A 1 Offset(0,42) A_SetPitch(pitch-(1.8*boa_recoilamount))
		BREN A 1 A_Refire
		BREN A 1 A_WeaponReady(WRF_NOBOB)
		BREN A 1 Offset(0,44) A_WeaponReady(WRF_NOBOB)
		BREN A 1 Offset(0,43) A_WeaponReady(WRF_NOBOB)
		BREN A 1 Offset(0,40) A_WeaponReady(WRF_NOBOB)
		BREN A 1 Offset(0,36) A_WeaponReady(WRF_NOBOB)
		BREN A 1 Offset(0,32) A_WeaponReady(WRF_NOBOB)
		Goto Ready
	Hold:
		BREN A 3
		BREN A 0 A_JumpIfInventory("BrenLoaded",1,1)
		Goto Dryfire
		BREN A 0 A_GunFlash
		BREN A 0 A_PlaySound("bren/fire",CHAN_WEAPON)
		BREN A 0 A_SpawnItemEx("EnfieldRifleCasing",12,-20,32,8,random(-2,2),random(0,4),random(-55,-80),SXF_NOCHECKPOSITION)
		BREN A 0 A_AlertMonsters
		BREN A 1 Offset(0,58) A_FireCustomMissile("BrenfieldTracer",frandom(-0.6,0.6))
		BREN A 0 A_JumpIf(waterlevel > 0,2)
		BREN A 0 A_FireProjectile("ChainSmokeSpawner",0,0,0,random(-4,4),0,0)
		BREN A 1 Offset(0,54) A_SetPitch(pitch-(1.8*boa_recoilamount))
		BREN A 1 A_Refire
		BREN A 1 Offset(0,52) A_WeaponReady(WRF_NOBOB)
		BREN A 1 Offset(0,54) A_WeaponReady(WRF_NOBOB)
		BREN A 1 Offset(0,53) A_WeaponReady(WRF_NOBOB)
		BREN A 1 Offset(0,50) A_WeaponReady(WRF_NOBOB)
		BREN A 1 Offset(0,46) A_WeaponReady(WRF_NOBOB)
		BREN A 1 Offset(0,41) A_WeaponReady(WRF_NOBOB)
		BREN A 1 Offset(0,35) A_WeaponReady(WRF_NOBOB)
		BREN A 1 Offset(0,32) A_WeaponReady(WRF_NOBOB)
		Goto Ready
	Reload:
		BREN A 1 Offset(0,35) A_PlaySound("BREN/reload",5)
		BREN A 1 Offset(0,38) 
		BREN A 1 Offset(0,44)
		BREN A 1 Offset(-2,46)
		BREN A 1 Offset(-4,52)
		BREN B 1 Offset(-7,56)
		BREN B 1 Offset(-10,57)
		BREN B 1 Offset(-11,58)
		BREN B 1 Offset(-2,46)
		BREN C 1 Offset(-4,52)
		BREN C 1 Offset(-7,56)
		BREN C 1 Offset(-10,57)
		BREN C 1 Offset(-10,65)
		BREN C 1 Offset(-11,58)
		BREN C 1 Offset(-2,46)
		BREN C 1 Offset(-4,52)
		BREN C 1 Offset(-7,56)
		BREN C 12 Offset(-11,58)
	ReloadLoop:
		BREN E 0 A_TakeInventory("303Ammo",1,TIF_NOTAKEINFINITE)
		BREN E 0 A_GiveInventory("BRENLoaded")
		BREN E 0 A_JumpIfInventory("BRENLoaded",0,"ReloadFinish")
		BREN E 0 A_JumpIfInventory("303Ammo",1,"ReloadLoop")
	ReloadFinish:
		BREN B 1 Offset(-10,57)
		BREN B 1 Offset(-11,58)
		BREN B 1 Offset(-2,46)
		BREN B 1 Offset(-4,52)
		BREN B 1 Offset(-7,56)
		BREN B 1 Offset(-10,58)
		BREN B 1 Offset(-10,61)
		BREN B 6 Offset(-10,65)
		BREN A 1 Offset(-10,71)
		BREN A 1 Offset(-10,65)
		BREN A 1 Offset(-10,60)
		BREN A 1 Offset(-10,55)
		BREN A 1 Offset(-10,53)
		BREN A 1 Offset(-10,51)
		BREN A 1 Offset(-9,50)
		BREN D 6 Offset(-8,46)
		BREN D 6 Offset(-7,43)
		BREN D 1 Offset(-5,40)
		BREN D 1 Offset(-3,37)
		BREN A 1 Offset(-1,34)
		BREN A 1 Offset(0,32)
		Goto Ready
	Flash:
		BREF A 1 BRIGHT A_Light2
		BREF B 1 BRIGHT
		TNT1 A 2 A_Light1
		Goto LightDone
	Spawn:
		BREW A -1
		Stop
	}
}

ACTOR BrenLoaded : Ammo
{
	Tag ".303 British"
	+IGNORESKILL
	Inventory.MaxAmount 30
	Inventory.Icon 303R02
}