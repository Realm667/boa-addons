// I will never forget how in Nazis!, the M1 Garand was loaded with a stripper clip instead of a proper en bloc. Weasel mentioned that it was done because there wasn't any custom sprites of the thing back then, so atleast it was understandable on why an already available stripper clip sprite frame is instead used. Still very amusing, though.

ACTOR EnBlocCase : Casing9mm
{
	Scale .1
	States
	{
	Spawn:
		CASB ABCDEFGH 2
		Loop
	}
}

ACTOR 3006Ammo : Ammo
{
	Tag ".30-06"
	Inventory.Amount 16
	Inventory.MaxAmount 40
	Ammo.BackpackAmount 24
	Ammo.BackpackMaxAmount 100
	Inventory.Icon GARA02
	Scale .1
	Inventory.PickupMessage "Picked up a .30-06 en bloc clip."
	States
	{
	Spawn:
		M1GA A -1
		Stop
	}
}

ACTOR 3006AmmoBox : 3006Ammo
{
	Inventory.Icon GARA02
	Inventory.Amount 40
	Inventory.PickupMessage "Picked up a box of .30-06 Springfield ammo."
	Scale 1
	States
	{
	Spawn:
		M1GA B -1
		Stop
	}
}

ACTOR M1GarandRifleCasing : MauserRifleCasing{}

ACTOR M1GarandTracer : PlayerTracer
{
	Damage (random(40,65))
	Speed 180
	ProjectileKickback 100
}

ACTOR Garand : NaziWeapon
{
	//$Category Weapons (Confiscated Weapons)
	//$Title (5) M1 Garand
	//$Color 14
	Scale 0.40
	Weapon.SlotNumber 5
	Weapon.AmmoType "GarandLoaded"
	Weapon.AmmoUse 1
	Weapon.AmmoType2 "3006Ammo"
	Weapon.AmmoUse2 1
	Weapon.AmmoGive1 8
	Weapon.AmmoGive2 20
	Inventory.PickupMessage "You got the M1 Garand!"
	Weapon.SelectionOrder 750
	+WEAPON.NOAUTOFIRE
	Tag "M1 Garand"
	Weapon.UpSound "garand/select"
	States
	{
	Ready:
		TNT1 A 0 A_JumpIfInventory("GarandLoaded",1,2)
		Goto ReadyEmpty
		TNT1 A 0 A_CheckReload
		TNT1 A 0 A_JumpIfInventory("GarandLoaded",0,2)
		TNT1 A 0 A_JumpIfInventory("3006Ammo",1,2)
		M1A1 A 1 A_WeaponReady
		Loop
		M1A1 A 1 A_WeaponReady(WRF_ALLOWRELOAD)
		Loop
	ReadyEmpty:
		TNT1 A 0 A_CheckReload
		TNT1 A 0 A_JumpIfInventory("3006Ammo",1,2)
		M1AR A 1 A_WeaponReady
		Goto Ready
		M1AR A 1 A_WeaponReady(WRF_ALLOWRELOAD)
		Goto Ready
	Select:
		TNT1 A 0 A_JumpIfInventory("GarandLoaded",1,3)
		M1AR A 0 A_Raise
		M1AR A 1 A_Raise
		Loop
		M1A1 A 0 A_Raise
		M1A1 A 1 A_Raise
		Loop
	Deselect:
		TNT1 A 0 A_JumpIfInventory("GarandLoaded",1,3)
		M1AR A 0 A_Lower
		M1AR A 1 A_Lower
		Loop
		M1A1 A 0 A_Lower
		M1A1 A 1 A_Lower
		Loop
	Fire:
		M1A1 A 0 A_JumpIfInventory("GarandLoaded",1,1)
		Goto Dryfire
		M1A1 A 0 A_GunFlash
		TNT1 A 0 A_PlaySound("garand/fire",CHAN_WEAPON)
		M1A1 A 0 A_SpawnItemEx("M1GarandRifleCasing",12,-20,32,8,random(-2,2),random(0,4),random(-55,-80),SXF_NOCHECKPOSITION)
		M1A1 A 0 A_AlertMonsters
		TNT1 A 0
		M1A1 A 1 A_FireCustomMissile("M1GarandTracer")
		M1A1 C 1 A_SetPitch(pitch-(3.6*boa_recoilamount))
		M1A1 C 0 A_JumpIf(waterlevel > 0,2)
		M1A1 C 0 A_FireProjectile("ShotSmokeSpawner",0,0,0,random(-4,4),0,0)
		M1A1 C 1 A_JumpIfInventory("GarandLoaded",1,3)
		Goto ClipEject
		M1A1 C 0 A_PlaySound("garand/ping",5)
		M1A1 C 0 A_GiveInventory("M1Pinged")
	FireFinish:
		M1A1 EDCBAAA 1
		TNT1 A 0 A_CheckReload
		Goto Ready
	ClipEject:
		M1A1 D 1 A_SpawnItemEx("EnBlocCase",12,-20,32,8,random(-2,2),random(0,4),random(-55,-80),SXF_NOCHECKPOSITION)
		M1AR A 1 A_PlaySound("garand/ping",5)
		M1AR A 1 A_GiveInventory("M1Pinged")
		Goto Ready
	Reload:
		M1A1 A 0 
		M1A1 A 5 A_JumpIfInventory("M1Pinged",1,"AfterUnload")
		M1A1 A 1 Offset(1,34)
		M1A1 A 1 Offset(2,36)
		M1A1 A 1 Offset(4,38)
		M1A1 A 1 Offset(6,42)
		M1A1 A 1 Offset(9,46)
		M1A1 A 1 Offset(12,50)
		M1A1 A 1 Offset(15,54)
		M1A1 A 1 Offset(19,59)
		M1A1 A 1 Offset(23,65) A_PlaySound("garand/unload",6)
		M1A1 B 1 Offset(28,72)
		M1A1 B 1 Offset(30,84)
		M1A1 C 1 Offset(31,89)
		M1A1 C 1 Offset(31,92)
		M1A1 D 1 Offset(31,94)
		M1A1 D 1 Offset(31,93)
		M1AR A 1 Offset(31,91)
		M1AR A 1 Offset(31,88)
		M1AR A 1 Offset(31,84)
		M1AR A 1 Offset(31,80)
		M1AR A 1 Offset(33,72)
		M1AR A 1 Offset(34,68)
		M1AR A 1 Offset(33,62)
		M1AR A 1 Offset(31,56)
		M1AR A 1 Offset(29,55)
		M1AR A 1 Offset(27,53)
		M1AR A 1 Offset(24,51)
		M1AR A 1 Offset(21,49)
		M1AR A 1 Offset(18,46)
		M1AR A 1 Offset(16,43)
		M1AR A 1 Offset(14,41)
		M1AR A 1 Offset(12,39)
		M1AR A 1 Offset(10,38)
		M1AR A 1 Offset(8,37)
		M1AR A 1 Offset(7,36)
		M1AR A 1 Offset(6,35)
		M1AR A 1 Offset(5,34)
		M1AR A 1 Offset(4,33)
		M1AR A 1 Offset(3,32)
		M1AR A 1 Offset(2,32)
	AfterUnload:
		M1AR AAAABBBBCCC 1
		M1AR D 1 A_PlaySound("garand/load",5)
		M1AR D 1
		M1AR E 1
		M1AR E 1
		M1AR G 1
		M1AR G 1
		M1AR H 1
		M1AR H 1
		M1AR H 1 Offset(-2,46)
		M1AR IJK 2
	ReloadLoop:
		M1AR A 0 A_TakeInventory("3006Ammo",1,TIF_NOTAKEINFINITE)
		M1AR A 0 A_GiveInventory("GarandLoaded")
		M1AR A 0 A_JumpIfInventory("GarandLoaded",0,"ReloadFinish")
		M1AR A 0 A_JumpIfInventory("3006Ammo",1,"ReloadLoop")
	ReloadFinish:
		M1AR L 1
		M1AR L 1
		M1A1 A 1 Offset(-2,46)
		M1A1 A 1 Offset(-5,40)
		M1A1 A 1 Offset(-3,37)
		M1A1 A 1 Offset(-1,34)
		M1A1 A 1
		M1A1 A 1
		M1A1 A 1
		M1A1 A 1
		M1A1 A 1
		M1A1 A 1
		M1A1 A 1 Offset(0,32)
		M1A1 A 1 A_TakeInventory("M1Pinged")
		M1A1 A 1 A_Refire
		Goto Ready
	Flash:
		M1AF A 1 BRIGHT A_Light2
		TNT1 A 1
		TNT1 A 2 A_Light1
		Goto LightDone
	Spawn:
		M1AP A -1
		Stop
	}
}

ACTOR GarandLoaded : Ammo
{
	Tag ".30-06"
	+IGNORESKILL
	Inventory.MaxAmount 8
	Inventory.Icon GARA01
}

ACTOR M1Pinged : Inventory{}