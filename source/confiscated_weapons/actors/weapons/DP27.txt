// Thought for a moment that this would be redundant, since you already got the Lewis for a plate mag LMG. The 30-to-40 change definitely contributed to why I ended up adding this one anyways. Yeah, before 40, I had initially wanted to stop at 30. Funny how things change like that.

ACTOR DP27Tracer : MosinTracer
{
	Speed 140
}

ACTOR DP27 : NaziWeapon
{
	//$Category Weapons (Confiscated Weapons)
	//$Title (8) Degtyaryov
	//$Color 14
	Scale 0.6
	Weapon.SlotNumber 8
	Weapon.SelectionOrder 750
	Weapon.AmmoType "DP27Loaded"
	Weapon.AmmoUse 1
	Weapon.AmmoType2 "NagantAmmo"
	Weapon.AmmoUse2 1
	Weapon.AmmoGive1 47
	Weapon.AmmoGive2 94
	Tag "DP-27"
	Inventory.PickupMessage "You got the DP-27!"
	Weapon.UpSound "degtyaryov/select"
	States
	{
	Select:
		D27G A 0 A_Raise
		D27G A 1 A_Raise
		Loop
	Deselect:
		D27G A 0 A_Lower
		D27G A 1 A_Lower
		Loop
	Ready:
		D27G A 0 A_JumpIfInventory("DP27Loaded",0,2)
		D27G A 0 A_JumpIfInventory("NagantAmmo",1,2)
		D27G A 1 A_WeaponReady
		Loop
		D27G A 1 A_WeaponReady(WRF_ALLOWRELOAD)
		Loop
	Fire:
		D27G A 0 A_JumpIfInventory("DP27Loaded",1,1)
		Goto Dryfire
		D27G A 0 A_GunFlash
		D27G A 0 A_PlaySound("degtyaryov/fire",CHAN_WEAPON)
		D27G A 0 A_SpawnItemEx("MosinCasing",12,-20,32,8,random(-2,2),random(0,4),random(-55,-80),SXF_NOCHECKPOSITION)
		D27G A 0 A_AlertMonsters
		D27G B 1 A_FireCustomMissile("DP27Tracer",frandom(-0.6,0.6))
		D27G B 0 A_JumpIf(waterlevel > 0,2)
		D27G B 0 A_FireProjectile("ShotSmokeSpawner",0,0,0,random(-4,4),0,0)
		D27G B 0 A_SetPitch(pitch-(1.8*boa_recoilamount))
		D27G B 1 Offset(0,42) A_Refire
		D27G A 1 A_WeaponReady(WRF_NOBOB)
		D27G A 1 Offset(0,44) A_WeaponReady(WRF_NOBOB)
		D27G A 1 Offset(0,43) A_WeaponReady(WRF_NOBOB)
		D27G A 1 Offset(0,40) A_WeaponReady(WRF_NOBOB)
		D27G A 1 Offset(0,36) A_WeaponReady(WRF_NOBOB)
		D27G A 1 Offset(0,32) A_WeaponReady(WRF_NOBOB)
		Goto Ready
	Hold:
		D27G A 3
		D27G A 0 A_JumpIfInventory("DP27Loaded",1,1)
		Goto Dryfire
		D27G A 0 A_GunFlash
		D27G A 0 A_PlaySound("degtyaryov/fire",CHAN_WEAPON)
		D27G A 0 A_SpawnItemEx("MosinCasing",12,-20,32,8,random(-2,2),random(0,4),random(-55,-80),SXF_NOCHECKPOSITION)
		D27G A 0 A_AlertMonsters
		D27G B 1 Offset(0,58) A_FireCustomMissile("DP27Tracer",frandom(-0.6,0.6))
		D27G B 0 A_JumpIf(waterlevel > 0,2)
		D27G B 0 A_FireProjectile("ShotSmokeSpawner",0,0,0,random(-4,4),0,0)
		D27G B 0 A_SetPitch(pitch-(1.8*boa_recoilamount))
		D27G B 1 Offset(0,54) A_Refire
		D27G A 1 Offset(0,52) A_WeaponReady(WRF_NOBOB)
		D27G A 1 Offset(0,54) A_WeaponReady(WRF_NOBOB)
		D27G A 1 Offset(0,53) A_WeaponReady(WRF_NOBOB)
		D27G A 1 Offset(0,50) A_WeaponReady(WRF_NOBOB)
		D27G A 1 Offset(0,46) A_WeaponReady(WRF_NOBOB)
		D27G A 1 Offset(0,41) A_WeaponReady(WRF_NOBOB)
		D27G A 1 Offset(0,35) A_WeaponReady(WRF_NOBOB)
		D27G A 1 Offset(0,32) A_WeaponReady(WRF_NOBOB)
		Goto Ready
	Reload:
		D27G A 1 Offset(0,35) A_PlaySound("degtyaryov/reload",5)
		D27G A 1 Offset(0,38) 
		D27G A 1 Offset(0,44)
		D27G A 1 Offset(-2,46)
		D27G A 1 Offset(-4,52)
		D27G C 1 Offset(-7,56)
		D27G C 1 Offset(-10,57)
		D27G C 1 Offset(-11,58)
		D27G C 1 Offset(-2,46)
		D27G D 1 Offset(-4,52)
		D27G D 1 Offset(-7,56)
		D27G D 1 Offset(-10,57)
		D27G D 1 Offset(-10,65)
		D27G D 1 Offset(-11,58)
		D27G D 1 Offset(-2,46)
		D27G D 1 Offset(-4,52)
		D27G D 1 Offset(-7,56)
		D27G D 12 Offset(-11,58)
	ReloadLoop:
		D27G E 0 A_TakeInventory("NagantAmmo",1,TIF_NOTAKEINFINITE)
		D27G E 0 A_GiveInventory("DP27Loaded")
		D27G E 0 A_JumpIfInventory("DP27Loaded",0,"ReloadFinish")
		D27G E 0 A_JumpIfInventory("NagantAmmo",1,"ReloadLoop")
	ReloadFinish:
		D27G D 1 Offset(-10,57)
		D27G D 1 Offset(-11,58)
		D27G D 1 Offset(-2,46)
		D27G D 1 Offset(-4,52)
		D27G D 1 Offset(-7,56)
		D27G D 1 Offset(-10,58)
		D27G D 1 Offset(-10,61)
		D27G C 6 Offset(-10,65)
		D27G C 1 Offset(-10,71)
		D27G C 1 Offset(-10,65)
		D27G C 1 Offset(-10,60)
		D27G C 1 Offset(-10,55)
		D27G C 1 Offset(-10,53)
		D27G C 1 Offset(-10,51)
		D27G C 1 Offset(-9,50)
		D27G E 6 Offset(-8,46)
		D27G E 6 Offset(-7,43)
		D27G E 1 Offset(-5,40)
		D27G E 1 Offset(-3,37)
		D27G A 1 Offset(-1,34)
		D27G A 1 Offset(0,32)
		Goto Ready
	Flash:
		D27F A 1 BRIGHT A_Light2
		D27F B 1 BRIGHT
		TNT1 A 2 A_Light1
		Goto LightDone
	Spawn:
		D27I A -1
		Stop
	}
}

ACTOR DP27Loaded : Ammo
{
	Tag "7.62x54mmR"
	+IGNORESKILL
	Inventory.MaxAmount 47
	Inventory.Icon PPSM03
}