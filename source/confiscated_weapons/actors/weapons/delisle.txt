// Yes, it is deliberate that this thing is incapable of killing enemies in one shot like a loud regular rifle would. Same as the Welrod. I'm in mind that firearms in a grounded environment shouldn't randomly alter space and time just because it's a different gun or fired in a different mode. Yes, I'm looking at you, vanilla Kar98K. Only reason why I didn't cripple the gun any further (from looking at the reduced velocity in ol' wikis) was because I'm too lazy to test results.

ACTOR DeLisleTracer : M1ThompsonTracer {}

ACTOR DeLisle : NaziWeapon
{
	//$Category Weapons (Confiscated Weapons)
	//$Title (5) De Lisle Carbine
	//$Color 14
	Scale 0.3
	Weapon.SlotNumber 5
	Weapon.SelectionOrder 750
	Weapon.AmmoType "DelisleLoaded"
	Weapon.AmmoUse 1
	Weapon.AmmoType2 "45ACPAmmo"
	Weapon.AmmoUse2 1
	Weapon.AmmoGive1 11
	Weapon.AmmoGive2 22
	Inventory.PickupMessage "You got the De Lisle Carbine!"
	Tag "De Lisle Carbine"
	+WEAPON.NOAUTOFIRE
	Weapon.UpSound "delisle/select"
	States
	{
	Select:
		DELC A 0 A_Raise
		DELC A 1 A_Raise
		Loop
	Deselect:
		DELC A 0 A_Lower
		DELC A 1 A_Lower
		Loop
	Ready:
		DELC A 0 A_JumpIfInventory("DelisleLoaded",0,2)
		DELC A 0 A_JumpIfInventory("45ACPAmmo",1,2)
		DELC A 1 A_WeaponReady
		Loop
		DELC A 1 A_WeaponReady(WRF_ALLOWRELOAD)
		Loop
	Fire:
		DELC A 0 A_JumpIfInventory("DelisleLoaded",1,1)
		Goto Dryfire
		DELC C 0 A_JumpIf(waterlevel > 0,2)
		DELC C 0 A_FireProjectile("ShotSmokeSpawner",0,0,0,random(-4,4),0,0)
		DELC A 0 A_PlaySound("delisle/fire",CHAN_WEAPON)
		DELC A 0 A_AlertMonsters(256)
		DELC B 1 A_FireCustomMissile("DeLisleTracer")
		DELC B 1 Offset(0,52)
		DELC B 1 Offset(0,45) A_SetPitch(pitch-(4.0*boa_recoilamount))
		DELC B 1 Offset(0,40) A_SetPitch(pitch-(2.0*boa_recoilamount))
		DELC B 1 Offset(0,36) A_SetPitch(pitch+(1.0*boa_recoilamount))
		DELC B 1 Offset(0,34) A_SetPitch(pitch+(1.0*boa_recoilamount))
		DELC B 1 Offset(0,33) A_SetPitch(pitch+(0.5*boa_recoilamount))
		DELC C 1 Offset(-2,33) A_PlaySound("delisle/boltback",5)
		DELC D 1 Offset(-4,34)
		DELC E 1 Offset(-6,35)
		DELC F 2 Offset(-8,36)
		DELC F 2 Offset(-4,42)
		DELC G 1 Offset(0,51) A_SpawnItemEx("45ACPCasing",12,-20,32,8,random(-2,2),random(0,4),random(-55,-80),SXF_NOCHECKPOSITION)
		DELC G 1 Offset(4,60)
		DELC G 2 Offset(5,74)
		DELC G 3 Offset(6,76)
		DELC F 2 Offset(5,74) A_PlaySound("delisle/boltfwd",5)
		DELC E 1 Offset(4,60)
		DELC D 1 Offset(0,51)
		DELC C 1 Offset(-4,42)
		DELC A 2 Offset(-8,36)
		DELC A 1 Offset(-6,35)
		DELC A 1 Offset(-4,34)
		DELC A 1 Offset(-2,33)
		TNT1 A 0 A_CheckReload
		Goto Ready
	Reload:
		TNT1 A 0 
		DELC A 1 Offset(0,35) A_PlaySound("delisle/reload",5)
		DELC C 1 Offset(-2,38)
		DELC C 1 Offset(-4,44)
		DELC D 1 Offset(-6,52)
		DELC D 1 Offset(-7,57)
		DELC E 1 Offset(-8,62)
		DELC E 1 Offset(-9,67)
		DELC F 1 Offset(-10,72)
		DELC F 1 Offset(-11,77)
		DELC G 1 Offset(-11,80) 
		DELC G 1 Offset(-12,82)
		DELC H 1 Offset(-12,84)
		DELC H 1 Offset(-13,86)
		DELC H 1 Offset(-13,87)
		DELC H 2 Offset(-14,88)
		DELC H 3 Offset(-14,89)
	ReloadLoop:
		TNT1 A 0 A_TakeInventory("45ACPAmmo",1,TIF_NOTAKEINFINITE)
		TNT1 A 0 A_GiveInventory("DelisleLoaded")
		TNT1 A 0 A_JumpIfInventory("DelisleLoaded",0,"ReloadFinish")
		TNT1 A 0 A_JumpIfInventory("45ACPAmmo",1,"ReloadLoop")
	ReloadFinish:
		DELC G 2 Offset(-10,96)
		DELC G 1 Offset(-6,100)
		DELC F 1 Offset(-7,90)
		DELC F 1 Offset(-6,80)
		DELC E 1 Offset(-4,70)
		DELC E 1 Offset(-2,60)
		DELC D 1 Offset(0,50)
		DELC C 1 Offset(0,40)
		DELC A 1 Offset(0,32)
		Goto Ready
	Spawn:
		DELN A -1
		Stop
	}
}

ACTOR DelisleLoaded : Ammo
{
	Tag ".45 ACP"
	+IGNORESKILL
	Inventory.MaxAmount 11
	Inventory.Icon FUDD01
}