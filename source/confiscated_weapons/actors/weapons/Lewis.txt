// Around the implementation of this gun, I had wondered where exactly I should put it. Pretty much all of the LMGs implemented so far were put in Slot 5, previously. But then I thought, aren't Slot 8 used for heavy weapons like the UMG43? Instead of stuffing the entire line up in Slot 5, bloating an already WAY plenty set of weapons, I ended moving the lot here at 8.

ACTOR LewisTracer : M1GarandTracer
{
	Speed 140
	ProjectileKickback 100
}

ACTOR LewisGun : NaziWeapon
{
	//$Category Weapons (Confiscated Weapons)
	//$Title (8) Lewis Gun
	//$Color 14
	Scale 0.5
	Weapon.SlotNumber 8
	Weapon.SelectionOrder 750
	Weapon.AmmoType "LewisLoaded"
	Weapon.AmmoUse 1
	Weapon.AmmoType2 "303Ammo"
	Weapon.AmmoUse2 1
	Weapon.AmmoGive1 47
	Weapon.AmmoGive2 94
	Tag "Lewis Gun"
	Inventory.PickupMessage "You got the Lewis Gun!"
	Weapon.UpSound "lewis/select"
	States
	{
	Select:
		LWSG A 0 A_Raise
		LWSG A 1 A_Raise
		Loop
	Deselect:
		LWSG A 0 A_Lower
		LWSG A 1 A_Lower
		Loop
	Ready:
		LWSG A 0 A_JumpIfInventory("LewisLoaded",0,2)
		LWSG A 0 A_JumpIfInventory("303Ammo",1,2)
		LWSG A 1 A_WeaponReady
		Loop
		LWSG A 1 A_WeaponReady(WRF_ALLOWRELOAD)
		Loop
	Fire:
		LWSG A 0 A_JumpIfInventory("LewisLoaded",1,1)
		Goto Dryfire
		LWSG A 0 A_GunFlash
		LWSG A 0 A_PlaySound("lewis/fire",CHAN_WEAPON)
		LWSG A 0 A_SpawnItemEx("M1GarandRifleCasing",12,-20,32,8,random(-2,2),random(0,4),random(-55,-80),SXF_NOCHECKPOSITION)
		LWSG A 0 A_AlertMonsters
		LWSG B 1 A_FireCustomMissile("LewisTracer",frandom(-0.6,0.6))
		LWSG B 0 A_JumpIf(waterlevel > 0,2)
		LWSG B 0 A_FireProjectile("ShotSmokeSpawner",0,0,0,random(-4,4),0,0)
		LWSG B 1 Offset(0,42) A_SetPitch(pitch-(1.8*boa_recoilamount))
		LWSG A 1 A_Refire
		LWSG A 1 A_WeaponReady(WRF_NOBOB)
		LWSG A 1 Offset(0,44) A_WeaponReady(WRF_NOBOB)
		LWSG A 1 Offset(0,43) A_WeaponReady(WRF_NOBOB)
		LWSG A 1 Offset(0,40) A_WeaponReady(WRF_NOBOB)
		LWSG A 1 Offset(0,36) A_WeaponReady(WRF_NOBOB)
		LWSG A 1 Offset(0,32) A_WeaponReady(WRF_NOBOB)
		Goto Ready
	Hold:
		LWSG A 3
		LWSG A 0 A_JumpIfInventory("LewisLoaded",1,1)
		Goto Dryfire
		LWSG A 0 A_GunFlash
		LWSG A 0 A_PlaySound("lewis/fire",CHAN_WEAPON)
		LWSG A 0 A_SpawnItemEx("M1GarandRifleCasing",12,-20,32,8,random(-2,2),random(0,4),random(-55,-80),SXF_NOCHECKPOSITION)
		LWSG A 0 A_AlertMonsters
		LWSG B 1 Offset(0,58) A_FireCustomMissile("LewisTracer",frandom(-0.6,0.6))
		LWSG B 0 A_JumpIf(waterlevel > 0,2)
		LWSG B 0 A_FireProjectile("ShotSmokeSpawner",0,0,0,random(-4,4),0,0)
		LWSG B 1 Offset(0,54) A_SetPitch(pitch-(1.8*boa_recoilamount))
		LWSG A 1 A_Refire
		LWSG A 1 Offset(0,52) A_WeaponReady(WRF_NOBOB)
		LWSG A 1 Offset(0,54) A_WeaponReady(WRF_NOBOB)
		LWSG A 1 Offset(0,53) A_WeaponReady(WRF_NOBOB)
		LWSG A 1 Offset(0,50) A_WeaponReady(WRF_NOBOB)
		LWSG A 1 Offset(0,46) A_WeaponReady(WRF_NOBOB)
		LWSG A 1 Offset(0,41) A_WeaponReady(WRF_NOBOB)
		LWSG A 1 Offset(0,35) A_WeaponReady(WRF_NOBOB)
		LWSG A 1 Offset(0,32) A_WeaponReady(WRF_NOBOB)
		Goto Ready
	Reload:
		LWSG A 1 Offset(0,35) A_PlaySound("lewis/reload",5)
		LWSG A 1 Offset(0,38) 
		LWSG A 1 Offset(0,44)
		LWSG A 1 Offset(-2,46)
		LWSG A 1 Offset(-4,52)
		LWSG C 1 Offset(-7,56)
		LWSG C 1 Offset(-10,57)
		LWSG C 1 Offset(-11,58)
		LWSG C 1 Offset(-2,46)
		LWSG D 1 Offset(-4,52)
		LWSG D 1 Offset(-7,56)
		LWSG D 1 Offset(-10,57)
		LWSG D 1 Offset(-10,65)
		LWSG D 1 Offset(-11,58)
		LWSG D 1 Offset(-2,46)
		LWSG D 1 Offset(-4,52)
		LWSG D 1 Offset(-7,56)
		LWSG D 12 Offset(-11,58)
	ReloadLoop:
		LWSG E 0 A_TakeInventory("303Ammo",1,TIF_NOTAKEINFINITE)
		LWSG E 0 A_GiveInventory("LewisLoaded")
		LWSG E 0 A_JumpIfInventory("LewisLoaded",0,"ReloadFinish")
		LWSG E 0 A_JumpIfInventory("303Ammo",1,"ReloadLoop")
	ReloadFinish:
		LWSG D 1 Offset(-10,57)
		LWSG D 1 Offset(-11,58)
		LWSG D 1 Offset(-2,46)
		LWSG D 1 Offset(-4,52)
		LWSG D 1 Offset(-7,56)
		LWSG D 1 Offset(-10,58)
		LWSG D 1 Offset(-10,61)
		LWSG C 6 Offset(-10,65)
		LWSG C 1 Offset(-10,71)
		LWSG C 1 Offset(-10,65)
		LWSG C 1 Offset(-10,60)
		LWSG C 1 Offset(-10,55)
		LWSG C 1 Offset(-10,53)
		LWSG C 1 Offset(-10,51)
		LWSG C 1 Offset(-9,50)
		LWSG E 6 Offset(-8,46)
		LWSG E 6 Offset(-7,43)
		LWSG E 1 Offset(-5,40)
		LWSG E 1 Offset(-3,37)
		LWSG A 1 Offset(-1,34)
		LWSG A 1 Offset(0,32)
		Goto Ready
	Flash:
		LWSF A 1 BRIGHT A_Light2
		LWSF B 1 BRIGHT
		TNT1 A 2 A_Light1
		Goto LightDone
	Spawn:
		LWSI A -1
		Stop
	}
}

ACTOR LewisLoaded : Ammo
{
	Tag ".303 British"
	+IGNORESKILL
	Inventory.MaxAmount 47
	Inventory.Icon PPSM03
}