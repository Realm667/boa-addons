// This funny gun was one of the weapons you could select in Hidden & Dangerous (a somewhat fond childhood memory, it's complicated), and so there's a slight ecstatic on my end when Jeimuzu spotted for me a model pack that has this thing in one of the folders. Unfortunately, there wasn't any proper credits for the blasted thing, the imbeciles that put together the model pack didn't bother at all to add any. I had to ask the folks at Half-Life Creations to see if some of them might knew anything about the model's origins. Thankfully, NormantheLoliPirate spotted that one for me. Real grateful for that one.

ACTOR JM1941 : NaziWeapon
{
	//$Category Weapons (Confiscated Weapons)
	//$Title (5) Johnson M1941
	//$Color 14
	Scale 0.32
	Weapon.SlotNumber 5
	Weapon.SelectionOrder 750
	Weapon.AmmoType "JM1941Loaded"
	Weapon.AmmoUse 1
	Weapon.AmmoType2 "3006Ammo"
	Weapon.AmmoUse2 1
	Weapon.AmmoGive1 10
	Weapon.AmmoGive2 20
	Inventory.PickupMessage "You got the Johnson M1941!"
	Tag "Johnson M1941"
	Weapon.UpSound "jm1941/select"
	+WEAPON.NOAUTOFIRE
	States
	{
	Select:
		JHSN A 0 A_Raise
		JHSN A 1 A_Raise
		Loop
	Deselect:
		JHSN A 0 A_Lower
		JHSN A 1 A_Lower
		Loop
	Ready:
		JHSN A 0 A_JumpIfInventory("JM1941Loaded",0,2)
		JHSN A 0 A_JumpIfInventory("3006Ammo",1,2)
		JHSN A 1 A_WeaponReady
		Loop
		JHSN A 1 A_WeaponReady(WRF_ALLOWRELOAD)
		Loop
	Fire:
		JHSN A 0 A_JumpIfInventory("JM1941Loaded",1,1)
		Goto Dryfire
		JHSN A 0 A_StartSound("jm1941/fire",CHAN_WEAPON)
		JHSN A 0 A_GunFlash
		JHSN A 0 A_SpawnItemEx("M1GarandRifleCasing",12,-20,32,8,random(-2,2),random(0,4),random(-55,-80),SXF_NOCHECKPOSITION)
		JHSN A 0 A_AlertMonsters
		JHSN B 1 A_FireCustomMissile("M1GarandTracer",frandom(-0.6,0.6))
		JHSN B 0 A_JumpIf(waterlevel > 0,2)
		JHSN B 0 A_FireProjectile("ShotSmokeSpawner",0,0,0,random(-4,4),0,0)
		JHSN B 2 Offset(0,40) A_SetPitch(pitch-(4.2*boa_recoilamount))
		JHSN B 1 Offset(0,36) A_SetPitch(pitch-(3.6*boa_recoilamount))
		JHSN B 1 Offset(0,32) A_SetPitch(pitch+(2.0*boa_recoilamount))
		JHSN B 1 A_SetPitch(pitch-(1.4*boa_recoilamount))
		JHSN B 1
		TNT1 A 0 A_CheckReload
		JHSN A 1 A_WeaponReady(WRF_NOBOB)
		Goto Ready
	Reload:
		JHSN A 0
		JHSN E 1 Offset(-2,33) 
		JHSN E 1 Offset(-4,34) 
		JHSN E 1 Offset(-6,35)
		JHSN E 2 Offset(-8,36)
		JHSN E 2 Offset(-4,42)
		JHSN E 1 Offset(0,51)
		JHSN E 1 Offset(4,60)
		JHSN E 2 Offset(5,74)
		JHSN E 3 Offset(6,76)
		JHSN E 2 Offset(5,74)
		JHSN E 8 Offset(6,76)
		JHSN A 0 A_JumpIfInventory("JM1941Loaded",5,"ReloadLoop2")
		JHSN A 0 A_JumpIfInventory("3006Ammo",5,1)
		Goto ReloadLoop2
		TNT1 A 0 A_TakeInventory("3006Ammo",5)
		TNT1 A 0 A_GiveInventory("JM1941Loaded",5)
		JHSN C 1 Offset(6,80) A_PlaySound("jm1941/insert",5)
		JHSN C 1 Offset(6,84)
		JHSN E 1 Offset(6,87)
		JHSN E 1 Offset(7,90)
		JHSN E 1 Offset(8,92)
		JHSN E 1 Offset(9,88)
		JHSN E 1 Offset(8,82)
		JHSN E 1 Offset(7,77)
		JHSN E 0 A_JumpIfInventory("JM1941Loaded",0,"ReloadOnlyOneClip")
		TNT1 A 0 A_JumpIfInventory("3006Ammo",1,1)
		Goto ReloadOnlyOneClip
		JHSN A 5 Offset(6,75)
	ReloadLoop2:
		TNT1 A 0 A_TakeInventory("3006Ammo",1)
		TNT1 A 0 A_GiveInventory("JM1941Loaded")
		TNT1 A 0 A_JumpIfInventory("JM1941Loaded",0,"ReloadFinish")
		TNT1 A 0 A_JumpIfInventory("3006Ammo",1,"ReloadLoop2")
	ReloadFinish:
		JHSN C 1 Offset(6,80) A_PlaySound("jm1941/insert",5)
		JHSN C 1 Offset(6,84)
		JHSN E 1 Offset(6,87)
		JHSN E 1 Offset(7,90)
		JHSN E 1 Offset(8,92)
		JHSN E 1 Offset(9,88)
		JHSN E 1 Offset(8,82)
		JHSN E 1 Offset(7,77)
	ReloadOnlyOneClip:
		JHSN D 2 Offset(5,74) A_PlaySound("jm1941/bolt",5)
		JHSN D 1 Offset(4,60)
		JHSN D 1 Offset(0,51)
		JHSN D 1 Offset(-4,42)
		JHSN A 2 Offset(-8,36)
		JHSN A 1 Offset(-6,35)
		JHSN A 1 Offset(-4,34)
		JHSN A 1 Offset(-2,33)
		Goto Ready
	Flash:
		JHSF A 1 BRIGHT A_Light2
		JHSF B 1 BRIGHT
		TNT1 A 2 A_Light1
		Goto LightDone
	Spawn:
		JHSW A -1
		Stop
	}
}

ACTOR JM1941Loaded : Ammo
{
	Tag ".30-06"
	+IGNORESKILL
	Inventory.MaxAmount 10
	Inventory.Icon GARA01
}