// BoA's grenades are surprisingly poor at dealing with room clearing, something that grenades have a bit of a reputation for. Sure, it's a balance thing to keep it from being an extremely reliable weapon, and splash damage grenades are also perfectly normal for shooters even today. However, the Ubersoldats can have a shrapnel grenade of their own, so now I definitely want one to match 'em.

ACTOR Mk2FragAmmo : Ammo
{
	Scale 0.25
	Tag "Components of Mk2 Frag Grenade"
	Inventory.Amount 1
	Inventory.MaxAmount 4
	Ammo.BackpackAmount 0
	Ammo.BackpackMaxAmount 9
	Inventory.PickupMessage "Someway, somehow, you managed to cobble up a whole new grenade out of spare parts while looking at the grenades you already have. Also, you're not actually supposed to see this, but I like to have fun with how people might handwave getting ammo for a weapon like this."
	Inventory.Icon MK2G01
	States
	{
	Spawn:
		MK2W A -1
		Stop
	}
}

ACTOR MK2Frag : NaziWeapon
{
	//$Category Weapons (Confiscated Weapons)
	//$Title (9) Mk2 Fragmentation Grenade
	//$Color 14
	Weapon.SlotNumber 9
	Weapon.SelectionOrder 10000
	Inventory.PickupMessage "You got an Mk2 Fragmentation Grenade!"
	Tag "Mk 2 Grenade"
	Weapon.AmmoUse 1
	Weapon.AmmoType "Mk2FragAmmo"
	Weapon.AmmoGive 2
	-WEAPON.AMMO_CHECKBOTH
	-WEAPON.NOALERT
	Scale 0.25
	Weapon.UpSound "grenade/select"
	Inventory.PickupSound "grenade/pickup"
	States
	{
	Select:
		MK2V A 0 A_Raise
		MK2V A 1 A_Raise
		Loop
	Deselect:
		TNT1 A 0 A_JumpIfInventory("Mk2FragAmmo",1,3)
		TNT1 A 0 A_Lower
		TNT1 A 1 A_Lower
		Loop
		MK2V A 0 A_Lower
		MK2V A 1 A_Lower
		Loop
	Ready:
		TNT1 A 0 A_JumpIfInventory("Mk2FragAmmo",1,2)
		TNT1 A 1 A_WeaponReady
		Loop
		MK2V A 1 A_WeaponReady
		Loop
	Fire:
		MK2V B 1 Offset(6,34)
		MK2V B 1 Offset(1,46)
		MK2V B 1 Offset(2,59)
		MK2V B 1 Offset(2,63)
		MK2V B 1 Offset(3,78)
		MK2V B 1 Offset(2,84)
		MK2V B 1 Offset(2,90) A_PlaySound("grenade/ringpull",CHAN_WEAPON)
		MK2V B 1 Offset(1,106) 
		MK2V B 1 Offset(1,114)
		MK2V B 1 Offset(6,122)
		MK2V B 1 Offset(3,130)
		TNT1 A 0 A_PlaySound("grenade/throw",CHAN_WEAPON)
		TNT1 A 2 Bright A_FireCustomMissile("Mk2Grenade_Thrown",0,1,0,0,0,0)
		TNT1 A 1 Offset(1,29)
		TNT1 A 1 Offset(-2,30)
		TNT1 A 1 Offset(-1,32)
		Goto Ready
	Spawn:
		MK2W A -1
		Loop
	}
}

Actor Mk2Grenade_Thrown : GrenadeBase
{
	Scale 0.25
	Radius 4
	Height 3
	Speed 40
	Damage (random(1,8))
	DamageType "Frag"
	Reactiontime 8 //for countdown
	Projectile
	-NOGRAVITY
	-NOTELEPORT
	+WINDTHRUST
	BounceType "Doom"
	BounceFactor 0.4
	WallBounceFactor 0.6
	BounceSound "grenade/bounce"
	States
	{
	Spawn:
		MK2T A 2 A_CountDown
		MK2T BCDEFGH 2
		TNT1 A 0 A_JumpIf(waterlevel == 3, "AdjustMass")
		Loop
	Swim:
		MK2T A 2 A_CountDown() 
		TNT1 A 0 A_ScaleVelocity(0.7) 
		TNT1 A 0 A_SpawnItemEx("Bubble", 0, 0, 0, 0, 0, 2, random (0, 360), 0, 128)
		MK2T BCDEFGH 2
		Loop
	Death:
		MK2T A 35
		TNT1 A 0 A_AlertMonsters
		TNT1 A 0 A_SetScale(1.75,1.75)
		TNT1 A 0 A_SetTranslucent(0.75,1)
		TNT1 A 0 A_StartSound("grenade/explode", CHAN_AUTO, 0, 1.0, 0.1)
		TNT1 A 1 A_SpawnItemEx("GeneralExplosion_Medium")
		TNT1 A 0 A_SpawnItemEx("KD_HL2SmokeGenerator")
		TNT1 A 0 A_SpawnItemEx("KD_HL2SparkGenerator")
		TNT1 AAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAA 0 A_SpawnitemEx("Mk2Grenade_Debris", 0, 0, 8, random(0,20), random(0,20), random(0,20), random(0,359), 0, 0)
		TNT1 A 1 Radius_Quake(10,10,0,16,0)
		Stop
	}
}

Actor Mk2Grenade_Debris
{
	Radius 16
	Height 16
	Mass 1
	Damage (random(4,16))
	Scale 0.25
	Gravity 1
	Projectile
	-NOGRAVITY
	+RANDOMIZE
	+ROLLSPRITE
	+HITOWNER
	States
	{
	Spawn:
		NLPJ A 4 NODELAY A_SetRoll(roll + 15, SPF_INTERPOLATE) 
		TNT1 A 0 A_JumpIf(waterlevel == 3, "AdjustMass")
		Loop
	AdjustMass: 
		NLPJ "#" 0 A_SetMass(400)
		Goto Swim
	Swim: 
		NLPJ A 2 A_ScaleVelocity(0.7)
		NLPJ A 2 A_SpawnItemEx("Bubble", 0, 0, 0, 0, 0, 2, random (0, 360), 0, 128)
		Loop
	Death:
		NLPJ A 0
		Stop
	}
}