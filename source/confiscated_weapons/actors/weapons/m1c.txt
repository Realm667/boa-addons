// I feel like I should add a counterpart to this rifle, a pistol or SMG maybe, but then I checked, and there wasn't any that the military ended up picking. This gun is literally the only thing that shoots .30 Carbine IRL in WW2. Well, I guess there's that one Thompson prototype, but I think we got enough Tommies here already.

ACTOR 30CarbineCasing : Casing9mm{}

ACTOR 30CarbineTracer : PlayerTracer
{
	ProjectileKickback 100
	Damage (random(28,36))
	Speed 180
}

ACTOR 30CarbineAmmo : Ammo
{
	Scale 0.35
	Tag ".30 Carbine"
	Inventory.Amount 30
	Inventory.MaxAmount 50
	Ammo.BackpackAmount 30
	Ammo.BackpackMaxAmount 100
	Inventory.PickupMessage "Picked up a mag of .30 Carbine ammo."
	Inventory.Icon M1AM01
	States
	{
	Spawn:
		M1AM A -1
		Stop
	}
}

ACTOR M1C : NaziWeapon
{
	//$Category Weapons (Confiscated Weapons)
	//$Title (5) M1A1 Carbine
	//$Color 14
	Scale 0.35
	Weapon.SlotNumber 5
	Weapon.SelectionOrder 750
	Weapon.AmmoType "M1CLoaded"
	Weapon.AmmoUse 1
	Weapon.AmmoType2 "30CarbineAmmo"
	Weapon.AmmoUse2 1
	Weapon.AmmoGive1 15
	Weapon.AmmoGive2 30
	Inventory.PickupMessage "You got the M1A1 Carbine!"
	Tag "M1A1 Carbine"
	Weapon.UpSound "m1c/select"
	+WEAPON.NOAUTOFIRE
	States
	{
	Select:
		M1CG A 0 A_Raise
		M1CG A 1 A_Raise
		Loop
	Deselect:
		M1CG A 0 A_Lower
		M1CG A 1 A_Lower
		Loop
	Ready:
		M1CG A 0 A_JumpIfInventory("M1CLoaded",0,2)
		M1CG A 0 A_JumpIfInventory("30CarbineAmmo",1,2)
		M1CG A 1 A_WeaponReady
		Loop
		M1CG A 1 A_WeaponReady(WRF_ALLOWRELOAD)
		Loop
	Fire:
		M1CG A 0 A_JumpIfInventory("M1CLoaded",1,1)
		Goto Dryfire
		M1CG A 0 A_StartSound("m1c/fire",CHAN_WEAPON)
		M1CG A 0 A_GunFlash
		M1CG A 0 A_SpawnItemEx("30CarbineCasing",12,-20,32,8,random(-2,2),random(0,4),random(-55,-80),SXF_NOCHECKPOSITION)
		M1CG A 0 A_AlertMonsters
		M1CG A 1 A_FireProjectile("30CarbineTracer")
		M1CG A 0 A_JumpIf(waterlevel > 0,2)
		M1CG A 0 A_FireProjectile("ShotSmokeSpawner",0,0,0,random(-4,4),0,0)
		M1CG A 1 A_SetPitch(pitch-(1.4*boa_recoilamount))
		M1CG CDEDC 1
		TNT1 A 0 A_CheckReload
		M1CG AAFFFA 1 A_WeaponReady(WRF_NOBOB)
		Goto Ready
	Reload:
		M1CG A 1 Offset(0,35) A_StartSound("m1c/reload",CHAN_5)
		M1CG A 1 Offset(-2,38)
		M1CG A 1 Offset(-4,44)
		M1CG A 1 Offset(-6,52)
		M1CG A 1 Offset(-7,57)
		M1CG G 1 Offset(-8,62)
		M1CG G 1 Offset(-9,67)
		M1CG G 1 Offset(-10,72)
		M1CG G 1 Offset(-11,77)
		M1CG G 1 Offset(-11,80)
		M1CG G 1 Offset(-12,82)
		M1CG G 1 Offset(-12,84)
		M1CG G 1 Offset(-13,86)
		M1CG G 1 Offset(-13,87)
		M1CG G 2 Offset(-14,88)
		M1CG G 3 Offset(-14,89)
	ReloadLoop:
		TNT1 A 0 A_TakeInventory("30CarbineAmmo",1,TIF_NOTAKEINFINITE)
		TNT1 A 0 A_GiveInventory("M1CLoaded")
		TNT1 A 0 A_JumpIfInventory("M1CLoaded",0,"ReloadFinish")
		TNT1 A 0 A_JumpIfInventory("30CarbineAmmo",1,"ReloadLoop")
	ReloadFinish:
		M1CG H 2 Offset(-10,96)
		M1CG H 1 Offset(-6,100)
		M1CG H 1 Offset(-7,90)
		M1CG H 1 Offset(-6,80)
		M1CG H 1 Offset(-4,70)
		M1CG H 1 Offset(-2,60)
		M1CG H 1 Offset(0,50)
		M1CG A 1 Offset(0,40)
		M1CG A 1 Offset(0,32)
		Goto Ready
	Flash:
		M1CF A 1 BRIGHT A_Light2
		M1CF B 1 BRIGHT
		TNT1 A 2 A_Light1
		Goto LightDone
	Spawn:
		M1CP A -1
		Stop
	}
}

ACTOR M1CLoaded : Ammo
{
	Tag ".30 Carbine"
	+IGNORESKILL
	Inventory.MaxAmount 15
	Inventory.Icon M1AM01
}